// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Получает сопоставленный статус ошибки по статусу задачи
//
// Параметры:
//  СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
//  СтатусОшибки - ОпределяемыйТип.канбан_СтатусыОшибок
//  Отказ - Булево
//
Процедура СопоставленныйСтатусОшибки(Знач СтатусЗадачи, СтатусОшибки, Отказ) Экспорт
	
	Условие = ЗначениеЗаполнено(СтатусЗадачи);
	Сообщение = НСтр("ru = 'Не заполнен статус задачи'");
	ОбщегоНазначенияКлиентСервер.Проверить(
		Условие, Сообщение, "РегистрСведений.канбан_СопоставлениеСтатусов.СопоставленныйСтатусОшибки");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	канбан_СопоставлениеСтатусов.СтатусОшибки КАК СтатусОшибки
		|ИЗ
		|	РегистрСведений.канбан_СопоставлениеСтатусов КАК канбан_СопоставлениеСтатусов
		|ГДЕ
		|	канбан_СопоставлениеСтатусов.СтатусЗадачи = &СтатусЗадачи";
	
	Запрос.УстановитьПараметр("СтатусЗадачи", СтатусЗадачи);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СтатусОшибки = ВыборкаДетальныеЗаписи.СтатусОшибки; // ОпределяемыйТип.канбан_СтатусыОшибок
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Получает сопоставленный статус задачи по статусу ошибки
//
// Параметры:
//  СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
//  СтатусОшибки - ОпределяемыйТип.канбан_СтатусыОшибок
//  Отказ - Булево
//
Процедура СопоставленныйСтатусЗадачи(СтатусЗадачи, Знач СтатусОшибки, Отказ) Экспорт
	
	Условие = ЗначениеЗаполнено(СтатусОшибки);
	Сообщение = НСтр("ru = 'Не заполнен статус ошибки'");
	ОбщегоНазначенияКлиентСервер.Проверить(
		Условие, Сообщение, "РегистрСведений.канбан_СопоставлениеСтатусов.СопоставленныйСтатусЗадачи");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	канбан_СопоставлениеСтатусов.СтатусЗадачи КАК СтатусЗадачи
		|ИЗ
		|	РегистрСведений.канбан_СопоставлениеСтатусов КАК канбан_СопоставлениеСтатусов
		|ГДЕ
		|	канбан_СопоставлениеСтатусов.СтатусОшибки = &СтатусОшибки";
	
	Запрос.УстановитьПараметр("СтатусОшибки", СтатусОшибки);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СтатусЗадачи = ВыборкаДетальныеЗаписи.СтатусЗадачи; // ОпределяемыйТип.канбан_СтатусыЗадач
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
