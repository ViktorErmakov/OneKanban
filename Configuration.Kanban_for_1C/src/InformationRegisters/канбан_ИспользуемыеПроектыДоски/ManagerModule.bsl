// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Записать.
// 
// Параметры:
//  ИспользуемыеПроектыДоски - см. канбан_КанбанДоска.НовыйПроектыДоски
//  Отказ - Булево
//
Процедура Записать(ИспользуемыеПроектыДоски, Отказ) Экспорт
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.канбан_ИспользуемыеПроектыДоски");
		ЭлементБлокировки.УстановитьЗначение("Пользователь", ТекущийПользователь);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
		
		Для Каждого ДанныеПроекта Из ИспользуемыеПроектыДоски Цикл
			
			СтрокаЗаписи = НаборЗаписей.Добавить();
			СтрокаЗаписи.Пользователь = ТекущийПользователь;
			СТрокаЗаписи.Проект = ДанныеПроекта.Проект;
			
			АбсолютныйЦвет = ДанныеПроекта.Цвет.ПолучитьАбсолютный();
			СтрокаЗаписи.Цвет = СтрШаблон(
				"%1, %2, %3", 
				Строка(АбсолютныйЦвет.Красный), 
				Строка(АбсолютныйЦвет.Зеленый), 
				Строка(АбсолютныйЦвет.Синий));
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(
			"Запись регистра сведений канбан_ИспользуемыеПроектыДоски для проекта: ", 
			УровеньЖурналаРегистрации.Ошибка,, 
			"канбан_ИспользуемыеПроектыДоски.Записать", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Загрузить.
// 
// Параметры:
//  ПроектыДоски - см. канбан_КанбанДоска.НовыйПроектыДоски
//
Процедура Загрузить(ПроектыДоски) Экспорт
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
	НаборЗаписей.Прочитать();
	
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		
		СтрокаПроектыДоски = ПроектыДоски.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПроектыДоски, СтрокаНабора, , "Цвет");
		СтрокаПроектыДоски.ПредставлениеПроекта = Строка(СтрокаНабора.Проект);
		СтрокаПроектыДоски.ИдентификаторПроектаСтрока = Строка(СтрокаНабора.Проект.УникальныйИдентификатор());
		СтрокаПроектыДоски.Цвет = ЦветИзСтроки(СтрокаНабора.Цвет);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЦветИзСтроки(ЦветСтрокой)
	
	Если ПустаяСтрока(ЦветСтрокой) Тогда
		
		Цвет = Новый Цвет;
		
	Иначе
		
		Цвета = СтрРазделить(ЦветСтрокой, ",");
		//@skip-check new-color
		Цвет = Новый Цвет(Цвета[0], Цвета[1], Цвета[2]);
		
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

#КонецОбласти

#КонецЕсли
