// @strict-types


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстHTMLКанбанДоски();
	
	ТипЗадач = Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип.Типы()[0];
	СправочникЗадачи = Метаданные.НайтиПоТипу(ТипЗадач);
	ИмяСправочникаЗадачи = СправочникЗадачи.ПолноеИмя();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ИмяПользователя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Наименование");
	ИдентификаторПользователя = Строка(ТекущийПользователь.УникальныйИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	События = канбан_КанбанДоскаКлиент.СобытияОбработкиОповещения();
	
	ДопустимоеСобытие = События.Найти(ИмяСобытия); // Строка
	Если ДопустимоеСобытие <> Неопределено Тогда
		
		Если ИмяСобытия = "ОбновитьКанбанДоскуHTML" Тогда
			
			Если ЗначениеЗаполнено(Параметр) Тогда
				
				ДанныеЗадачи = канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски();
				канбан_КанбанДоскаВызовСервера.ЗаполнитьДанныеЗадачиДляОтвета(Параметр, ДанныеЗадачи);
				
				Если ПустаяСтрока(ДанныеЗадачи.idTask) Тогда
					
					// При снятии галки отображения на доске у задачи, обновляем всю страницу
					ТекстHTMLКанбанДоски();
					
				Иначе
					
					ОтправитьОтветСозданиеОбновлениеЗадачи(ДанныеЗадачи);
					
				КонецЕсли;
				
			Иначе
				
				// При обновлении проектов, кнопка обновления, изменении настроек доски: сопоставление статусов и запись настроек
				ТекстHTMLКанбанДоски();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Канбан доска при нажатии.
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент
//  ДанныеСобытия - Структура:
//  * Href - Строка
//  * Button - Структура:
//  ** type - Строка
//  ** id - Строка
//  ** className - Строка
//  ** value - Строка
//  ** attributes - Массив из Структура:
//  *** name - Строка
//  *** value - Строка
//  ** parentElement - Структура:
//  *** id - Строка
//  *** parentElement - Структура:
//  **** id - Строка
//  * Element - Структура:
//  ** className - Строка
//  ** fullnameobjectstatus - Строка
//      
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
&НаКлиенте
Процедура КанбанДоскаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда
		
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ДанныеСобытия.Href);
		
		Возврат;
		
	КонецЕсли;
	
	Кнопка = ?(ДанныеСобытия.Button = Неопределено, ДанныеСобытия.Element, ДанныеСобытия.Button); // оставить только btn
	
	Если Кнопка.id = "V8_request" Тогда
		
		СтандартнаяОбработка = Ложь;
		ИзмененнаяЗадача = Неопределено;
		
		НастройкиДоски = канбан_КанбанДоскаКлиент.НовыйНастройкиДоски();
		НастройкиДоскиПолученные = Кнопка.attributes;
		Для Каждого Настройка Из НастройкиДоскиПолученные Цикл
			
			Если НастройкиДоски.Свойство(Настройка.name) Тогда
				
				Если Настройка.name = "executorfilter" 
					Или Настройка.name = "projectfilter" Тогда
					
					НастройкиДоски[Настройка.name] = канбан_КанбанДоскаВызовСервера.JSONВЗначение(Настройка.value, , Ложь);
					
				Иначе
					
					НастройкиДоски[Настройка.name] = Настройка.value;
					
				КонецЕсли;
				
			ИначеЕсли Настройка.name = "task" Тогда
				
				ИзмененнаяЗадача = канбан_КанбанДоскаВызовСервера.JSONВЗначение(Настройка.value, , Ложь); // Структура
				
			КонецЕсли;
			
		КонецЦикла;
		
		НастройкиДоски.currentUserId = ИдентификаторПользователя;
		НастройкиДоски.currentUserName = ИмяПользователя;
	
		канбан_КанбанДоскаКлиент.СохранитьТекущиеНастройкиДоски(НастройкиДоски);
		
		Если Кнопка.value = "changeStatus" Тогда
			
			// Точечное изменение данных задачи на доске
			ДанныеЗадачи = канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски();
			ИзменитьСтатусЗадаче(ИзмененнаяЗадача, ДанныеЗадачи);
			ОтправитьОтветСозданиеОбновлениеЗадачи(ДанныеЗадачи);
			
		ИначеЕсли Кнопка.value = "refresh" Тогда
			
			Оповестить("ОбновитьКанбанДоскуHTML", , ЭтотОбъект);
			
		КонецЕсли;
		
	ИначеЕсли Кнопка.id = "setup_svg" Тогда // переделать верстку как btn
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.НастройкиДоски");
		
	ИначеЕсли Кнопка.id = "add_task" Тогда // переделать верстку как btn
		
		КлассыЭлемента = СтрРазделить(Кнопка.className, " ");
		
		СтандартнаяОбработка = Ложь;
		
		ИдентификаторСтатусаСтрока = СтрЗаменить(КлассыЭлемента[1], "status", "");
		ИмяМенеджераОбъектаЗадача = Кнопка.attributes[2].value;
		СтатусЗадачиСсылка = СтатусЗадачиСсылка(ИмяМенеджераОбъектаЗадача, ИдентификаторСтатусаСтрока);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Статус", СтатусЗадачиСсылка); // Управление задач не заполняет параметры.
		
		ИмяФормыСправочникаЗадачи = СтрШаблон("%1.ФормаОбъекта", ИмяСправочникаЗадачи);
		
		ОткрытьФорму(ИмяФормыСправочникаЗадачи, ПараметрыОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанДоскаДокументСформирован(Элемент)
	
	ОбновитьТекущиеНастройкиДоскиНаФорме();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТекстHTMLКанбанДоски()
	
	канбан_КанбанДоска.ТекстHTMLКанбанДоски(КанбанДоска, УникальныйИдентификатор);
	
КонецПроцедуры

// Изменяет статус задачи на канбан-доске.
//
// Параметры:
//   ИзмененнаяЗадача - Структура:
//   * idtask - Строка
//   * fullnameobjecttask - Строка
//   * idnewstatus - Строка
//   * fullNameObjectStatus - Строка
//   * idNewExecutor - Строка
//   ДанныеЗадачи - см. канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски
//
&НаКлиенте
Процедура ИзменитьСтатусЗадаче(ИзмененнаяЗадача, ДанныеЗадачи)
	
	ИдентификаторЗадача = СтрЗаменить(ИзмененнаяЗадача["idtask"], "task", "");
	ИмяМенеджераОбъектаЗадача = ИзмененнаяЗадача["fullnameobjecttask"];
	
	ИдентификаторСтатуса = ИзмененнаяЗадача["idnewstatus"];
	ИмяМенеджераОбъектаСтатус = ИзмененнаяЗадача["fullNameObjectStatus"];
	ИдентификаторНовогоИсполнителя = СтрЗаменить(ИзмененнаяЗадача["idNewExecutor"], "user", "");
	
	ДанныеПриИзмененииСтатуса = канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса();
	ДанныеПриИзмененииСтатуса.ИдентификаторЗадача = ИдентификаторЗадача;
	ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаЗадача = ИмяМенеджераОбъектаЗадача;
	ДанныеПриИзмененииСтатуса.ИдентификаторСтатуса = ИдентификаторСтатуса;
	ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаСтатус = ИмяМенеджераОбъектаСтатус;
	ДанныеПриИзмененииСтатуса.ИдентификаторИсполнителя = ИдентификаторНовогоИсполнителя;
	
	Отказ = Ложь;
	ЗадачаСсылка = ПриИзмененииСтатусаЗадаче(ДанныеПриИзмененииСтатуса, ДанныеЗадачи, Отказ);
	
	Если Отказ Тогда
		
		ОповещениеПослеОтветаНаВопрос = Новый ОписаниеОповещения("ОповещениеПослеОтветаНаВопрос", ЭтотОбъект);
		Сообщение = СтрШаблон(НСтр("ru = 'Не сопоставлен выбранный статус доски со статусом ошибки, сопоставить?'"));
		
		ДополнительныеПараметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ДополнительныеПараметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
			ОповещениеПослеОтветаНаВопрос, Сообщение, РежимДиалогаВопрос.ДаНет, ДополнительныеПараметры);
		
	Иначе
		
		ОповеститьОбИзменении(ЗадачаСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

// Канбан доска при изменении статуса задаче.
// 
// Параметры:
//  ДанныеПриИзмененииСтатуса - см. канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса
//  ДанныеЗадачи - см. канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски
//  Отказ - Булево
//
&НаСервереБезКонтекста
Функция ПриИзмененииСтатусаЗадаче(Знач ДанныеПриИзмененииСтатуса, ДанныеЗадачи, Отказ)
	
	ЗадачаСсылка = канбан_КанбанДоска.ИзменитьСтатусЗадаче(ДанныеПриИзмененииСтатуса, Отказ);
	
	Обработки.Канбан_Доска_HTML.ЗаполнитьДанныеЗадачиДляОтвета(ЗадачаСсылка, ДанныеЗадачи);
	
	Возврат ЗадачаСсылка;
	
КонецФункции

// Статус задачи ссылка.
// 
// Параметры:
//  ИмяМенеджераОбъектаЗадача - Строка - Имя менеджера объекта задача
//  ИдентификаторСтатусаСтрока - Строка - Идентификатор статуса строка
// 
// Возвращаемое значение:
//  ОпределяемыйТип.канбан_СтатусыЗадач
//
&НаСервереБезКонтекста
Функция СтатусЗадачиСсылка(Знач ИмяМенеджераОбъектаЗадача, Знач ИдентификаторСтатусаСтрока)
	
	Возврат канбан_КанбанДоска.СсылкаНаОбъектПоИдентификатору(ИмяМенеджераОбъектаЗадача, ИдентификаторСтатусаСтрока);
	
КонецФункции

// Обработка результата ответа на вопрос пользователя.
//
// Параметры:
//   Результат - Структура:
//    * Значение - КодВозвратаДиалога - результат ответа пользователя на вопрос
//    * БольшеНеЗадаватьЭтотВопрос - Булево
//   ДополнительныеПараметры - Произвольный - дополнительные параметры, переданные при вызове обработчика
//
&НаКлиенте
Процедура ОповещениеПослеОтветаНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Значение = КодВозвратаДиалога.Да Тогда
		
		канбан_КанбанДоскаКлиент.ОткрытьФормуНастройкиДоски();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущиеНастройкиДоскиНаФорме()
	
	НастройкиДоски = канбан_КанбанДоскаКлиент.ЗагрузитьТекщиеНастройкиДоски();
	
	Если НастройкиДоски <> Неопределено Тогда
		НастройкиДоски.currentUserId = ИдентификаторПользователя;
		НастройкиДоски.currentUserName = ИмяПользователя;
	КонецЕсли;
	
	канбан_КанбанДоскаКлиент.ОтправитьОтвет(Элементы.КанбанДоска, "SettingsUpdate", НастройкиДоски);
	
КонецПроцедуры

// Отправить ответ создание обновление задачи.
// 
// Параметры:
//  ДанныеЗадачи - см. канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски
//
&НаКлиенте
Процедура ОтправитьОтветСозданиеОбновлениеЗадачи(ДанныеЗадачи)
	
	// При создании новой задачи, изменении статуса из задачи
	СписокЗадач = Новый Массив; // Массив из см. канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски
	НастройкиДоски = Новый Структура;
	НастройкиДоски.Вставить("tasks", СписокЗадач);
	НастройкиДоски.tasks.Добавить(ДанныеЗадачи);
	
	канбан_КанбанДоскаКлиент.ОтправитьОтвет(
		Элементы.КанбанДоска, 
		"createTask", 
		НастройкиДоски);
	
КонецПроцедуры

#КонецОбласти