// @strict-types


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстHTMLКанбанДоскиНаСервере();
	
	ТипЗадач = Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип.Типы()[0];
	СправочникЗадачи = Метаданные.НайтиПоТипу(ТипЗадач);
	ИмяСправочникаЗадачи = СправочникЗадачи.ПолноеИмя();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	События = канбан_КанбанДоскаКлиент.СобытияОбработкиОповещения();
	
	ДопустимоеСобытие = События.Найти(ИмяСобытия); // Строка
	Если ДопустимоеСобытие <> Неопределено Тогда
		
		Если ИмяСобытия = "ОбновитьКанбанДоскуHTML" Тогда
			
			КанбанДоска = "";
			
		КонецЕсли;
		
		ТекстHTMLКанбанДоски();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Канбан доска при нажатии.
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент
//  ДанныеСобытия - Структура:
//  * Href - Строка
//  * Button - Структура:
//  ** type - Строка
//  ** id - Строка
//  ** className - Строка
//  ** parentElement - Структура:
//  *** id - Строка
//  *** parentElement - Структура:
//  **** id - Строка
//  * Element - Структура:
//  ** className - Строка
//      
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
&НаКлиенте
Процедура КанбанДоскаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда
		
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ДанныеСобытия.Href);
		
		Возврат;
		
	КонецЕсли;
	
	Кнопка = ?(ДанныеСобытия.Button = Неопределено, ДанныеСобытия.Element, ДанныеСобытия.Button);
	Если Не ЗначениеЗаполнено(Кнопка) 
		// Пропускаем обработку для быстрого фильтра проектов
		Или Кнопка.type = "checkbox" Тогда
		Возврат;
	КонецЕсли;
	
	Если Кнопка.id = "V8_request" Тогда
		
		Если Кнопка.value = "changeStatus" Тогда
			
			СтандартнаяОбработка = Ложь;
			ИзменитьСтатусЗадаче(Кнопка);
			
		КонецЕсли;
		
	ИначеЕсли Кнопка.id = "update_svg" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПодключитьОбработчикОжидания("ТекстHTMLКанбанДоски", 0.1, Истина);
		
	ИначеЕсли Кнопка.id = "setup_svg" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.НастройкиДоски");
		
	ИначеЕсли Кнопка.id = "add_task" Тогда
		
		КлассыЭлемента = СтрРазделить(Кнопка.className, " ");
		
		СтандартнаяОбработка = Ложь;
		
		ИдентификаторСтатусаСтрока = СтрЗаменить(КлассыЭлемента[1], "status", "");
		ИмяМенеджераОбъектаЗадача = Кнопка.attributes["fullnameobjectstatus"].nodeValue;
		СтатусЗадачиСсылка = СтатусЗадачиСсылка(ИмяМенеджераОбъектаЗадача, ИдентификаторСтатусаСтрока);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Статус", СтатусЗадачиСсылка); // Управление задач не заполняет параметры.
		
		ИмяФормыСправочникаЗадачи = СтрШаблон("%1.ФормаОбъекта", ИмяСправочникаЗадачи);
		
		ОткрытьФорму(ИмяФормыСправочникаЗадачи, ПараметрыОткрытия);
		
	ИначеЕсли Кнопка.id = "my_tasks" Тогда
		
		СтандартнаяОбработка = Ложь;
		
	ИначеЕсли Кнопка.id = "theme_toggle" Тогда
		
		ТемнаяТема = Кнопка.classList.value = "theme_toggle active";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТекстHTMLКанбанДоски()
	
	Если Не ПустаяСтрока(КанбанДоска) Тогда
		
		ЭлементКанбанДоска = Элементы.КанбанДоска.Документ;
		КанбанДоска = ЭлементКанбанДоска.documentElement.innerHTML;
		
	КонецЕсли;
	
	ТекстHTMLКанбанДоскиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТекстHTMLКанбанДоскиНаСервере()
	
	канбан_КанбанДоска.ТекстHTMLКанбанДоски(КанбанДоска, ТемнаяТема, УникальныйИдентификатор);
	
КонецПроцедуры

// Изменяет статус задачи на канбан-доске.
//
// Параметры:
//   Кнопка - Структура:
//   * attributes - Массив из Структура:
//   ** nodeValue - Строка
//
&НаКлиенте
Процедура ИзменитьСтатусЗадаче(Кнопка)
	
	ИдентификаторЗадача = СтрЗаменить(Кнопка.attributes["idtask"].nodeValue, "task", "");
	ИмяМенеджераОбъектаЗадача = Кнопка.attributes["fullnameobjecttask"].nodeValue;
	
	ИдентификаторСтатуса = Кнопка.attributes["idnewstatus"].nodeValue;
	ИмяМенеджераОбъектаСтатус = Кнопка.attributes["fullnameobjectstatus"].nodeValue;
	
	ДанныеПриИзмененииСтатуса = канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса();
	ДанныеПриИзмененииСтатуса.ИдентификаторЗадача = ИдентификаторЗадача;
	ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаЗадача = ИмяМенеджераОбъектаЗадача;
	ДанныеПриИзмененииСтатуса.ИдентификаторСтатуса = ИдентификаторСтатуса;
	ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаСтатус = ИмяМенеджераОбъектаСтатус;
	
	Отказ = Ложь;
	ЗадачаСсылка = КанбанДоскаПриИзмененииСтатусаЗадаче(ДанныеПриИзмененииСтатуса, Отказ);
	
	Если Отказ Тогда
		
		ОповещениеПослеОтветаНаВопрос = Новый ОписаниеОповещения("ОповещениеПослеОтветаНаВопрос", ЭтотОбъект);
		Сообщение = СтрШаблон(НСтр("ru = 'Не сопоставлен выбранный статус доски со статусом ошибки, сопоставить?'"));
		
		ДополнительныеПараметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ДополнительныеПараметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
			ОповещениеПослеОтветаНаВопрос, Сообщение, РежимДиалогаВопрос.ДаНет, ДополнительныеПараметры);
		
	Иначе
		
		ОповеститьОбИзменении(ЗадачаСсылка);
		
	КонецЕсли;
	
	Оповестить("канбан_ИзменениеСтатусаЗадаче", ЗадачаСсылка);
	
КонецПроцедуры

// Канбан доска при изменении статуса задаче.
// 
// Параметры:
//  ДанныеПриИзмененииСтатуса - см. канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса
//  Отказ - Булево
//
&НаСервере
Функция КанбанДоскаПриИзмененииСтатусаЗадаче(ДанныеПриИзмененииСтатуса, Отказ)
	
	ЗадачаСсылка = канбан_КанбанДоска.ИзменитьСтатусЗадаче(ДанныеПриИзмененииСтатуса, Отказ);
	
	Возврат ЗадачаСсылка;
	
КонецФункции

// Статус задачи ссылка.
// 
// Параметры:
//  ИмяМенеджераОбъектаЗадача - Строка - Имя менеджера объекта задача
//  ИдентификаторСтатусаСтрока - Строка - Идентификатор статуса строка
// 
// Возвращаемое значение:
//  ОпределяемыйТип.канбан_СтатусыЗадач
//
&НаСервере
Функция СтатусЗадачиСсылка(Знач ИмяМенеджераОбъектаЗадача, Знач ИдентификаторСтатусаСтрока)
	
	Возврат канбан_КанбанДоска.СтатусОбъектаПоИдентификатору(ИмяМенеджераОбъектаЗадача, ИдентификаторСтатусаСтрока);
	
КонецФункции

// Обработка результата ответа на вопрос пользователя.
//
// Параметры:
//   Результат - Структура:
//    * Значение - КодВозвратаДиалога - результат ответа пользователя на вопрос
//    * БольшеНеЗадаватьЭтотВопрос - Булево
//   ДополнительныеПараметры - Произвольный - дополнительные параметры, переданные при вызове обработчика
//
&НаКлиенте
Процедура ОповещениеПослеОтветаНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Значение = КодВозвратаДиалога.Да Тогда
		
		канбан_КанбанДоскаКлиент.ОткрытьФормуНастройкиДоски();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти