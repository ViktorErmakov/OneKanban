// @strict-types


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстHTMLКанбанДоскиНаСервере();
	
	ТипЗадач = Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип.Типы()[0];
	СправочникЗадачи = Метаданные.НайтиПоТипу(ТипЗадач);
	ИмяСправочникаЗадачи = СправочникЗадачи.ПолноеИмя();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ИмяПользователя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Наименование");
	ИдентификаторПользователя = Строка(ТекущийПользователь.УникальныйИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	//TODO: нужно поймать изменение задачи, или создание новой из других форм
	// и отправить сообщение на доску
	
	События = канбан_КанбанДоскаКлиент.СобытияОбработкиОповещения();
	
	ДопустимоеСобытие = События.Найти(ИмяСобытия); // Строка
	Если ДопустимоеСобытие <> Неопределено Тогда
		
//		Если ИмяСобытия = "ОбновитьКанбанДоскуHTML" Тогда
//			
//			КанбанДоска = "";
//			ТекстHTMLКанбанДоски();
//			
//		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Канбан доска при нажатии.
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент
//  ДанныеСобытия - Структура:
//  * Href - Строка
//  * Button - Структура:
//  ** type - Строка
//  ** id - Строка
//  ** className - Строка
//  ** parentElement - Структура:
//  *** id - Строка
//  *** parentElement - Структура:
//  **** id - Строка
//  * Element - Структура:
//  ** className - Строка
//      
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
&НаКлиенте
Процедура КанбанДоскаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда
		
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ДанныеСобытия.Href);
		
		Возврат;
		
	КонецЕсли;
	
	Кнопка = ?(ДанныеСобытия.Button = Неопределено, ДанныеСобытия.Element, ДанныеСобытия.Button); // оставить только btn
	
	Если Кнопка.id = "V8_request" Тогда
		
		СтандартнаяОбработка = Ложь;
		ИзмененнаяЗадача = Неопределено;
		СохранитьТекущиеНастройкиДоски(Кнопка.attributes, ИзмененнаяЗадача);
		
		Если Кнопка.value = "changeStatus" Тогда
			
			ДанныеЗадачи = НовыйДанныеЗадачиДляОтвета();
			ИзменитьСтатусЗадаче(ИзмененнаяЗадача, ДанныеЗадачи);
			
			НастройкиДоски = Новый Структура;
			НастройкиДоски.Вставить("tasks", Новый Массив);
			НастройкиДоски.tasks.Добавить(ДанныеЗадачи);
			
			канбан_КанбанДоскаКлиент.ОтправитьОтвет(
				Элементы.КанбанДоска, 
				Кнопка.value, 
				НастройкиДоски);
			
		ИначеЕсли Кнопка.value = "refresh" Тогда
			
			ПодключитьОбработчикОжидания("ТекстHTMLКанбанДоски", 0.1, Истина);
			
		КонецЕсли;
		
	ИначеЕсли Кнопка.id = "setup_svg" Тогда // переделать верстку как btn
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.НастройкиДоски");
		
	ИначеЕсли Кнопка.id = "add_task" Тогда // переделать верстку как btn
		
		КлассыЭлемента = СтрРазделить(Кнопка.className, " ");
		
		СтандартнаяОбработка = Ложь;
		
		ИдентификаторСтатусаСтрока = СтрЗаменить(КлассыЭлемента[1], "status", "");
		ИмяМенеджераОбъектаЗадача = Кнопка.attributes["fullnameobjectstatus"].nodeValue;
		СтатусЗадачиСсылка = СтатусЗадачиСсылка(ИмяМенеджераОбъектаЗадача, ИдентификаторСтатусаСтрока);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Статус", СтатусЗадачиСсылка); // Управление задач не заполняет параметры.
		
		ИмяФормыСправочникаЗадачи = СтрШаблон("%1.ФормаОбъекта", ИмяСправочникаЗадачи);
		
		ОткрытьФорму(ИмяФормыСправочникаЗадачи, ПараметрыОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КанбанДоскаДокументСформирован(Элемент)
	
	ОбновитьТекущиеНастройкиДоскиНаФорме();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТекстHTMLКанбанДоски()
	
	Если Не ПустаяСтрока(КанбанДоска) Тогда
		
		ЭлементКанбанДоска = Элементы.КанбанДоска.Документ;
		КанбанДоска = ЭлементКанбанДоска.documentElement.innerHTML;
		
	КонецЕсли;
	
	ТекстHTMLКанбанДоскиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТекстHTMLКанбанДоскиНаСервере()
	
	канбан_КанбанДоска.ТекстHTMLКанбанДоски(КанбанДоска, УникальныйИдентификатор);
	
КонецПроцедуры

// Изменяет статус задачи на канбан-доске.
//
// Параметры:
//   ИзмененнаяЗадача - Строка
//   ДанныеЗадачи - см. НовыйДанныеЗадачиДляОтвета
//
&НаКлиенте
Процедура ИзменитьСтатусЗадаче(ИзмененнаяЗадача, ДанныеЗадачи)
	
	ИдентификаторЗадача = СтрЗаменить(ИзмененнаяЗадача["idtask"], "task", "");
	ИмяМенеджераОбъектаЗадача = ИзмененнаяЗадача["fullnameobjecttask"];
	
	ИдентификаторСтатуса = ИзмененнаяЗадача["idnewstatus"];
	ИмяМенеджераОбъектаСтатус = ИзмененнаяЗадача["fullNameObjectStatus"];
	ИдентификаторНовогоИсполнителя = СтрЗаменить(ИзмененнаяЗадача["idNewExecutor"], "user", "");
	
	ДанныеПриИзмененииСтатуса = канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса();
	ДанныеПриИзмененииСтатуса.ИдентификаторЗадача = ИдентификаторЗадача;
	ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаЗадача = ИмяМенеджераОбъектаЗадача;
	ДанныеПриИзмененииСтатуса.ИдентификаторСтатуса = ИдентификаторСтатуса;
	ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаСтатус = ИмяМенеджераОбъектаСтатус;
	ДанныеПриИзмененииСтатуса.ИдентификаторИсполнителя = ИдентификаторНовогоИсполнителя;
	
	Отказ = Ложь;
	ЗадачаСсылка = ПриИзмененииСтатусаЗадаче(ДанныеПриИзмененииСтатуса, ДанныеЗадачи, Отказ);
	
	Если Отказ Тогда
		
		ОповещениеПослеОтветаНаВопрос = Новый ОписаниеОповещения("ОповещениеПослеОтветаНаВопрос", ЭтотОбъект);
		Сообщение = СтрШаблон(НСтр("ru = 'Не сопоставлен выбранный статус доски со статусом ошибки, сопоставить?'"));
		
		ДополнительныеПараметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ДополнительныеПараметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
			ОповещениеПослеОтветаНаВопрос, Сообщение, РежимДиалогаВопрос.ДаНет, ДополнительныеПараметры);
		
	Иначе
		
		ОповеститьОбИзменении(ЗадачаСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

// Канбан доска при изменении статуса задаче.
// 
// Параметры:
//  ДанныеПриИзмененииСтатуса - см. канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса
//  ДанныеЗадачи - см. НовыйДанныеЗадачиДляОтвета
//  Отказ - Булево
//
&НаСервереБезКонтекста
Функция ПриИзмененииСтатусаЗадаче(Знач ДанныеПриИзмененииСтатуса, ДанныеЗадачи, Отказ)
	
	ЗадачаСсылка = канбан_КанбанДоска.ИзменитьСтатусЗадаче(ДанныеПриИзмененииСтатуса, Отказ);
	
	ЗаполнитьДанныеЗадачиДляОтвета(ЗадачаСсылка, ДанныеЗадачи);
	
	Возврат ЗадачаСсылка;
	
КонецФункции

// Статус задачи ссылка.
// 
// Параметры:
//  ИмяМенеджераОбъектаЗадача - Строка - Имя менеджера объекта задача
//  ИдентификаторСтатусаСтрока - Строка - Идентификатор статуса строка
// 
// Возвращаемое значение:
//  ОпределяемыйТип.канбан_СтатусыЗадач
//
&НаСервереБезКонтекста
Функция СтатусЗадачиСсылка(Знач ИмяМенеджераОбъектаЗадача, Знач ИдентификаторСтатусаСтрока)
	
	Возврат канбан_КанбанДоска.СсылкаНаОбъектПоИдентификатору(ИмяМенеджераОбъектаЗадача, ИдентификаторСтатусаСтрока);
	
КонецФункции

// Обработка результата ответа на вопрос пользователя.
//
// Параметры:
//   Результат - Структура:
//    * Значение - КодВозвратаДиалога - результат ответа пользователя на вопрос
//    * БольшеНеЗадаватьЭтотВопрос - Булево
//   ДополнительныеПараметры - Произвольный - дополнительные параметры, переданные при вызове обработчика
//
&НаКлиенте
Процедура ОповещениеПослеОтветаНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Значение = КодВозвратаДиалога.Да Тогда
		
		канбан_КанбанДоскаКлиент.ОткрытьФормуНастройкиДоски();
		
	КонецЕсли;
	
КонецПроцедуры

// Сохранить текущие настройки доски.
// 
// Параметры:
//  НастройкиДоскиПолученные - Структура:
//   * name - Строка
//   * value - Строка
//  ИзмененнаяЗадача - Строка - данные измененных задач
//
&НаКлиенте
Процедура СохранитьТекущиеНастройкиДоски(НастройкиДоскиПолученные, ИзмененнаяЗадача)
	
	НастройкиДоски = НовыйНастройкиДоски();
	
	Для Каждого Настройка Из НастройкиДоскиПолученные Цикл
		
		Если НастройкиДоски.Свойство(Настройка.name) Тогда
			
			Если Настройка.name = "executorfilter" 
				Или Настройка.name = "projectfilter" Тогда
				
				НастройкиДоски[Настройка.name] = канбан_КанбанДоскаВызовСервера.JSONВЗначение(Настройка.value, , Ложь);
				
			Иначе
				
				НастройкиДоски[Настройка.name] = Настройка.value;
				
			КонецЕсли;
			
		ИначеЕсли Настройка.name = "task" Тогда
			
			ИзмененнаяЗадача = канбан_КанбанДоскаВызовСервера.JSONВЗначение(Настройка.value, , Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
	НастройкиДоски.currentUserId = ИдентификаторПользователя;
	НастройкиДоски.currentUserName = ИмяПользователя;
	
	КлючОбъекта = КлючОбъектаНастроекДоски();
	КлючНастроек = КлючНастроекДоски();
	ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, НастройкиДоски);
	
КонецПроцедуры

// Настройки доски.
// 
// Возвращаемое значение:
//  Структура - Новый настройки доски:
// * theme - Строка - тема доски
// * grouping - Строка - группировка доски
// * executorfilter - Массив из Строка - фильтр исполнителя
// * projectfilter - Массив из Строка - фильтр проекта
// * currentuserid - Строка - идентификатор текущего пользователя
// * currentusername - Строка - имя текущего пользователя
//
&НаКлиенте
Функция НовыйНастройкиДоски()
	
	Результат = Новый Структура;
	Результат.Вставить("theme", "");
	Результат.Вставить("grouping", "");
	Результат.Вставить("executorfilter", Новый Массив);
	Результат.Вставить("projectfilter", Новый Массив);
	Результат.Вставить("currentuserid", "");
	Результат.Вставить("currentusername", "");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючОбъектаНастроекДоски()
	
	Возврат "OneKanban";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючНастроекДоски()
	
	Возврат "OneKanbanSettings";
	
КонецФункции

&НаКлиенте
Процедура ОбновитьТекущиеНастройкиДоскиНаФорме()
	
	НастройкиДоски = ЗагрузитьТекщиеНастройкиДоски();
	
	канбан_КанбанДоскаКлиент.ОтправитьОтвет(Элементы.КанбанДоска, "SettingsUpdate", НастройкиДоски);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузитьТекщиеНастройкиДоски()
	
	КлючОбъекта = КлючОбъектаНастроекДоски();
	КлючНастроек = КлючНастроекДоски();
	НастройкиДоски = ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНастроек); // см. НовыйНастройкиДоски
	НастройкиДоски.currentUserId = ИдентификаторПользователя;
	НастройкиДоски.currentUserName = ИмяПользователя;
	
	Возврат НастройкиДоски;
	
КонецФункции

// Заполнить данные задачи для ответа.
// 
// Параметры:
//  ЗадачаСсылка - ОпределяемыйТип.канбан_Задачи, ОпределяемыйТип.канбан_Ошибки, ПланВидовРасчетаСсылкаИмяПланаВидовРасчета, ПланСчетовСсылкаИмяПланаСчетов, ПланВидовХарактеристикСсылкаИмяПланаВидовХарактеристик, ДокументСсылкаИмяДокумента, ПланОбменаСсылкаИмяПланаОбмена, СправочникСсылкаИмяСправочника, БизнесПроцессСсылкаИмяБизнесПроцесса, ЗадачаСсылкаИмяЗадачи - Задача ссылка
//  ДанныеЗадачи - см. НовыйДанныеЗадачиДляОтвета
//
&НаСервереБезКонтекста
Процедура ЗаполнитьДанныеЗадачиДляОтвета(Знач ЗадачаСсылка, ДанныеЗадачи)
	
//	ДанныеДляДоски = канбан_КанбанДоска.ДанныеДляКанбанДоски();
	
	ТекстЗапросаДанныеЗадач = канбан_КанбанДоскаПереопределяемый.ТекстЗапросаДанныеЗадач();
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаДанныеЗадач);
	ПервыйПакет = СхемаЗапроса.ПакетЗапросов.Получить(СхемаЗапроса.ПакетЗапросов.Количество() -1);
	ПервыйПакет.Операторы.Удалить(ПервыйПакет.Операторы.Количество() - 1);
	Оператор = ПервыйПакет.Операторы[0];
	Оператор.Отбор.Добавить("Задачи.Ссылка = &ЗадачаСсылка");
	
	ТексЗапросаИтоговый = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТексЗапросаИтоговый;
	Запрос.УстановитьПараметр("ЗадачаСсылка", ЗадачаСсылка);
	Выборка = Запрос.Выполнить();
	РезультатЗапроса = Выборка.Выбрать();
	РезультатЗапроса.Следующий();
	
	КартинкаПоУмолчанию = БиблиотекаКартинок.канбан_ФотоПоУмолчанию;
	ДвоичныеДанныеФотографииПоУмолчанию = КартинкаПоУмолчанию.ПолучитьДвоичныеДанные();
	
	СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(РезультатЗапроса.Задача);
	СсылкаНаИБ = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
	ДанныеЗадачи.card__link_href = СтрШаблон("%1#%2", СсылкаНаИБ, СсылкаНаОбъект);
	ДанныеЗадачи.card__link_name = 
		СтрШаблон("%1 №%2", 
			РезультатЗапроса.Задача.Метаданные().ПредставлениеОбъекта, 
			РезультатЗапроса.КодЗадачи);
	
	ДанныеЗадачи.card__photo = канбан_КанбанДоска.ЗаполнитьФотографиюИсполнителя(
		РезультатЗапроса.ХранилищеФотографии, ДвоичныеДанныеФотографииПоУмолчанию);
	ДанныеЗадачи.alt = РезультатЗапроса.ПредставлениеПользователя;
	ДанныеЗадачи.card__text = РезультатЗапроса.НаименованиеЗадачи;
	ДанныеЗадачи.fullnameobjecttask = РезультатЗапроса.Задача.Метаданные().ПолноеИмя();
	ДанныеЗадачи.idTask = СтрШаблон("task%1", Строка(РезультатЗапроса.Задача.УникальныйИдентификатор()));
	ДанныеЗадачи.newStatus = Строка(РезультатЗапроса.СтатусЗадачи.УникальныйИдентификатор());
	ДанныеЗадачи.project = СтрШаблон("project%1", Строка(РезультатЗапроса.УникальныйИдентификаторПроекта));
	ДанныеЗадачи.user = СтрШаблон("user%1", Строка(РезультатЗапроса.Исполнитель.УникальныйИдентификатор()));
	
	ИмяПользователяБезПробелов = СтрЗаменить(РезультатЗапроса.ПредставлениеПользователя, " ", "_");
	ДанныеЗадачи.user_name = СтрШаблон("user_name%1", ИмяПользователяБезПробелов);
	
КонецПроцедуры

// Новый данные задачи для ответа.
// 
// Возвращаемое значение:
//  Структура - Новый данные задачи для ответа:
// * project - Строка
// * user - Строка
// * user_name - Строка
// * idTask - Строка
// * fullnameobjecttask - Строка
// * card__link_href - Строка
// * card__link_name - Строка
// * card__photo - Строка
// * alt - Строка
// * card__text - Строка
// * newStatus - Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция НовыйДанныеЗадачиДляОтвета()
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("project", "");
	ДанныеОтвета.Вставить("user", "");
	ДанныеОтвета.Вставить("user_name", "");
	ДанныеОтвета.Вставить("idTask", "");
	ДанныеОтвета.Вставить("fullnameobjecttask", "");
	ДанныеОтвета.Вставить("card__link_href", "");
	ДанныеОтвета.Вставить("card__link_name", "");
	ДанныеОтвета.Вставить("card__photo", "");
	ДанныеОтвета.Вставить("alt", "");
	ДанныеОтвета.Вставить("card__text", "");
	ДанныеОтвета.Вставить("newStatus", "");
	
	Возврат ДанныеОтвета;
	
КонецФункции

#КонецОбласти