// @strict-types


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользуемыеСтатусыДоскиЗагрузить();
	
	ИспользуемыеПроектыДоскиЗагрузить();
	
	ИспользоватьСопоставлениеСтатусов = Константы.канбан_ИспользоватьСопоставлениеСтатусов.Получить();
	
	АдресЛоготипа = БиблиотекаКартинок.канбан_Доска;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресЛоготипаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	канбан_КанбанДоскаКлиент.ОткрытьСтраницуРазработки();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСопоставлениеСтатусовПриИзменении(Элемент)
	
	ИспользоватьСопоставлениеСтатусовПриИзмененииСервер();
	
	УстановитьВидимостьДоступность();
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтатусыЗадач

&НаКлиенте
Процедура СтатусыЗадачПриИзменении(Элемент)
	
	Номер = 1;
	Для Каждого СтрокаСтатуса Из СтатусыЗадач Цикл
		СтрокаСтатуса.НомерПоПорядку = Номер;
		Номер = Номер + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИспользуемыеПроектыДоски

&НаКлиенте
Процедура ИспользуемыеПроектыДоскиЦветНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ИспользуемыеПроектыДоскиЦветНачалоВыбораПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуемыеПроектыДоскиЦветПриИзменении(Элемент)
	
	ИспользуемыеПроектыДоскиЦветПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьПродолжение();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьПродолжение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИспользуемыеСтатусыДоскиЗагрузить()
	
	ИспользуемыеСтатусы = канбан_КанбанДоска.ИспользуемыеСтатусыДоскиЗагрузить();
	Если ЗначениеЗаполнено(ИспользуемыеСтатусы) Тогда
		
		СтатусыЗадач.Загрузить(ИспользуемыеСтатусы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	ИспользуемыеПроектыДоскиЦветПриИзмененииСервер();
	
	Элементы.СтраницаСопоставление.Видимость = ИспользоватьСопоставлениеСтатусов;
	
КонецПроцедуры

&НаСервере
Процедура ИспользуемыеПроектыДоскиЗагрузить()
	
	ПроектыЗагруженные = канбан_КанбанДоска.ИспользуемыеПроектыДоскиЗагрузить();
	Если ЗначениеЗаполнено(ПроектыЗагруженные) Тогда
		
		ПроектыРеквизит = РеквизитФормыВЗначение("ИспользуемыеПроектыДоски");
		Для Каждого СтрокаПроекта Из ПроектыЗагруженные Цикл
			
			НоваяСтрокаПроектыРеквизит = ПроектыРеквизит.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПроектыРеквизит, СтрокаПроекта);
			
		КонецЦикла;
		ЗначениеВРеквизитФормы(ПроектыРеквизит, "ИспользуемыеПроектыДоски");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПродолжение()
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	ЗаписатьНаСервере(Отказ);
	
	Если Не Отказ Тогда
		Модифицированность = Отказ;
	КонецЕсли;
	
	Оповестить("ОбновитьКанбанДоскуHTML", , ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере(Отказ)
	
	ЗаписатьСтатусыЗадач(Отказ);
	ЗаписатьИспользуемыеПроектыДоски(Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСтатусыЗадач(Отказ)
	
	Статусы = СтатусыЗадач.Выгрузить();
	
	канбан_КанбанДоска.ИспользуемыеСтатусыДоскиСохранить(Статусы, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИспользуемыеПроектыДоски(Отказ)
	
	ПроектыДоски = РеквизитФормыВЗначение("ИспользуемыеПроектыДоски");
	канбан_КанбанДоска.ИспользуемыеПроектыДоскиСохранить(ПроектыДоски, Отказ);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ИспользуемыеПроектыДоскиЦветНачалоВыбораПродолжение()
	
	ТекущиеДанные = Элементы.ИспользуемыеПроектыДоски.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// TODO Нужно цвет хранить в поле с типом цвет, а сохранять в текстовом варианте
	// т.е. нужно цвет уметь преобразовать в строку, обратно из строки есть функция ниже ЦветИзСтроки
	
	ЦветДоВыбора = ТекущиеДанные.Цвет;
	
	ВыборЦвета = Новый ДиалогВыбораЦвета;
	ВыборЦвета.Цвет = ЦветИзСтроки(ЦветДоВыбора);
	Ждать ВыборЦвета.ВыбратьАсинх();
	
	АбсолютныйЦвет = ВыборЦвета.Цвет.ПолучитьАбсолютный();
	Красный = ?(АбсолютныйЦвет.Красный = 0, "0", АбсолютныйЦвет.Красный);
	Зеленый = ?(АбсолютныйЦвет.Зеленый = 0, "0", АбсолютныйЦвет.Зеленый);
	Синий = ?(АбсолютныйЦвет.Синий = 0, "0", АбсолютныйЦвет.Синий);
	
	ТекущиеДанные.Цвет = СтрШаблон("%1,%2,%3", Красный, Зеленый, Синий);
	
	Если ТекущиеДанные.Цвет <> ЦветДоВыбора Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	ИспользуемыеПроектыДоскиЦветПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ИспользуемыеПроектыДоскиЦветПриИзмененииСервер()
	
	Для Каждого СтрокаПроекта Из ИспользуемыеПроектыДоски Цикл
		
		Если Не ПустаяСтрока(СтрокаПроекта.Цвет) Тогда
			
			ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
			
			ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользуемыеПроектыДоски.Цвет");
			ЭлементОтбораДанных.ВидСравнения  = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбораДанных.ПравоеЗначение = СтрокаПроекта.Цвет;
			ЭлементОтбораДанных.Использование = Истина;
			
			ЭлементОформляемогоПоля = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ЭлементОформляемогоПоля.Поле = Новый ПолеКомпоновкиДанных("ИспользуемыеПроектыДоскиЦвет");
			ЭлементОформляемогоПоля.Использование = Истина;
			
			Цвет = ЦветИзСтроки(СтрокаПроекта.Цвет);
			
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Цвет);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЦветИзСтроки(ЦветСтрокой)
	
	Если ПустаяСтрока(ЦветСтрокой) Тогда
		
		Цвет = Новый Цвет;
		
	Иначе
		
		Цвета = СтрРазделить(ЦветСтрокой, ",");
		Цвет = Новый Цвет(Цвета[0], Цвета[1], Цвета[2]);
		
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаписатьПродолжение();
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьСопоставлениеСтатусовПриИзмененииСервер()
	
	ИспользоватьСопоставлениеСтатусовЗапись();
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьСопоставлениеСтатусовЗапись()
	
	ИспользоватьСопоставлениеСтатусовСохраненное = Константы.канбан_ИспользоватьСопоставлениеСтатусов.Получить();
	Если ИспользоватьСопоставлениеСтатусовСохраненное <> ИспользоватьСопоставлениеСтатусов Тогда
		
		Константы.канбан_ИспользоватьСопоставлениеСтатусов.Установить(ИспользоватьСопоставлениеСтатусов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
