// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить текст HTML.
// 
// Параметры:
//  ТекстКанбанДоска - Строка - реквизит HTML страницы
//  УникальныйИдентификатор - УникальныйИдентификатор - гуид формы
//  ПоказыватьФотоПользователя - Булево - Показывать фото пользователя
//
Процедура ТекстHTMLКанбанДоски(
		ТекстКанбанДоска, Знач УникальныйИдентификатор, Знач ПоказыватьФотоПользователя = Истина) Экспорт
	
	ТекстКанбанДоска = "";
	
	ДанныеДляДоски = канбан_КанбанДоска.ДанныеДляКанбанДоски();
	
	Если ДанныеДляДоски.Статусы.Количество() = 0 Тогда
		
		ДвоичныеДанныеСтраницаРекламы = ПолучитьМакет("ШаблонПустаяДоскаHTML");
		канбан_КанбанДоска.ЗагрузитьПриложение(ДвоичныеДанныеСтраницаРекламы, ТекстКанбанДоска, УникальныйИдентификатор);
		
		Возврат;
		
	КонецЕсли;
	
	ДвоичныеДанныеСтраницаРекламы = ПолучитьМакет("ШаблонКанбанДоскаHTML");
	ТекстДоскиИзМакета = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеСтраницаРекламы);
	ДокументHTML = канбан_КанбанДоска.ПолучитьОбъектДокументHTMLИзТекстаHTML(ТекстДоскиИзМакета);
	
	УзелЧекбоксы = ДокументHTML.ПолучитьЭлементПоИдентификатору("checkboxes");
	
	ПроектыДоски = ДанныеДляДоски.Проекты;
	Для Каждого ДанныеПроекта Из ПроектыДоски Цикл
		
		УзелЧекбоксПроект = ДокументHTML.СоздатьЭлемент("div");
		УзелЧекбоксПроект.УстановитьАтрибут("class", "checkbox");
		
		ИД_Проекта = СтрШаблон("project%1", ДанныеПроекта.ИдентификаторПроектаСтрока);
		
		//Input
		УзелИнпутЧекбокса = ДокументHTML.СоздатьЭлемент("input");
		УзелИнпутЧекбокса.УстановитьАтрибут("type", "checkbox");
		УзелИнпутЧекбокса.УстановитьАтрибут("id", ИД_Проекта);
		УзелИнпутЧекбокса.УстановитьАтрибут("checked", "true");
		УзелЧекбоксПроект.ДобавитьДочерний(УзелИнпутЧекбокса);
		
		//Label
		УзелЛейблЧекбокса = ДокументHTML.СоздатьЭлемент("label");
		УзелЛейблЧекбокса.УстановитьАтрибут("for", ИД_Проекта);
			
			//Вложенный div
			УзелЧекбоксИмя = ДокументHTML.СоздатьЭлемент("div");
			УзелЧекбоксИмя.УстановитьАтрибут("class", СтрШаблон("tag %1", ИД_Проекта));
			
			Если ЗначениеЗаполнено(ДанныеПроекта.Цвет) Тогда
				ЦветСтрокой = канбан_КанбанДоска.СтрокаИзЦвета(ДанныеПроекта.Цвет);
				УзелЧекбоксИмя.УстановитьАтрибут("style", СтрШаблон("background-color: rgb(%1);", ЦветСтрокой));
			КонецЕсли;
			
			УзелЧекбоксИмя.ТекстовоеСодержимое = Строка(ДанныеПроекта.ПредставлениеПроекта);
			УзелЛейблЧекбокса.ДобавитьДочерний(УзелЧекбоксИмя);
		
		УзелЧекбоксПроект.ДобавитьДочерний(УзелЛейблЧекбокса);
		
		УзелЧекбоксы.ДобавитьДочерний(УзелЧекбоксПроект);
		
	КонецЦикла;
	
	УзелЗакрепленнаяШапка = ДокументHTML.ПолучитьЭлементПоИдентификатору("sticky_blok");
	
	УзелДоска = ДокументHTML.ПолучитьЭлементПоИдентификатору("kanban-board");
	УзелДоска.ТекстовоеСодержимое = "";
	
	// Узел для фиксированной шапки статусов задач
	УзелКанбанХедер = ДокументHTML.ПолучитьЭлементПоИдентификатору("kanban_header");
	УзелКанбанХедер.ТекстовоеСодержимое = "";
	
	// Узел для самих задач внутри статусов доски
	УзелКанбанТело = ДокументHTML.СоздатьЭлемент("div");
	УзелКанбанТело.УстановитьАтрибут("class", "kanban_body");
	
	Для Каждого ДанныеСтатуса Из ДанныеДляДоски.Статусы Цикл
		
		УникальныйИдентификаторСтатуса = СтрЗаменить(ДанныеСтатуса.УникальныйИдентификатор, " ", "");
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Статус", ДанныеСтатуса.СтатусЗадачи);
		ЗадачиСоСтатусом = ДанныеДляДоски.ЗадачиИСтатусы.НайтиСтроки(ПараметрыОтбора);
		
		УзелБлокХедера = ДокументHTML.СоздатьЭлемент("div");
		УзелБлокХедера.УстановитьАтрибут("class", "block_header");
		
		// Наименование статуса
		УзелРазделаИмяСтатуса = ДокументHTML.СоздатьЭлемент("strong");
		УзелРазделаИмяСтатуса.УстановитьАтрибут("class", "kanban-block__name");
		УзелРазделаИмяСтатуса.ТекстовоеСодержимое = СтрШаблон("%1", Строка(ДанныеСтатуса.Представление));
		УзелБлокХедера.ДобавитьДочерний(УзелРазделаИмяСтатуса);
		
		// Количество задач
		УзелРазделаКоличествоЗадач = ДокументHTML.СоздатьЭлемент("strong");
		УзелРазделаКоличествоЗадач.УстановитьАтрибут("class", "kanban-block__number");
		УзелРазделаКоличествоЗадач.ТекстовоеСодержимое = СтрШаблон("%1", Строка(ЗадачиСоСтатусом.Количество()));
		УзелБлокХедера.ДобавитьДочерний(УзелРазделаКоличествоЗадач);
		
		#Область КнопкаДобавитьЗадачу
		
		Если ДанныеСтатуса.КнопкаДобавить Тогда
			
			ИдентификаторСтатуса = СтрШаблон("status%1", УникальныйИдентификаторСтатуса);
			
			// Блок кнопки добавления задачи
			УзелСсылкаЗадача = ДокументHTML.СоздатьЭлемент("div");
//			УзелСсылкаЗадача.УстановитьАтрибут("href", "#");
			УзелСсылкаЗадача.УстановитьАтрибут("id", "add_task");
			УзелСсылкаЗадача.УстановитьАтрибут("class", СтрШаблон("add_task %1", ИдентификаторСтатуса));
			УзелСсылкаЗадача.УстановитьАтрибут("fullNameObjectStatus", ДанныеСтатуса.СтатусЗадачи.Метаданные().ПолноеИмя());
			
			УзелСсылкаСВГ = ДокументHTML.СоздатьЭлемент("svg");
			УзелСсылкаСВГ.УстановитьАтрибут("viewBox", "0 0 24 24");
			
			УзелСсылкаPath = ДокументHTML.СоздатьЭлемент("path");
			УзелСсылкаPath.УстановитьАтрибут("d", "M12 4V20M4 12H20");
			УзелСсылкаPath.УстановитьАтрибут("stroke", "rgb(121, 121, 121)");
			УзелСсылкаPath.УстановитьАтрибут("stroke-width", "2");
			УзелСсылкаPath.УстановитьАтрибут("stroke-linecap", "round");
			
			УзелСсылкаСВГ.ДобавитьДочерний(УзелСсылкаPath);
			УзелСсылкаЗадача.ДобавитьДочерний(УзелСсылкаСВГ);
			УзелБлокХедера.ДобавитьДочерний(УзелСсылкаЗадача);
			
		КонецЕсли;
		
		#КонецОбласти
		
		УзелКанбанХедер.ДобавитьДочерний(УзелБлокХедера);
		
		УзелРазделаСтатуса = ДокументHTML.СоздатьЭлемент("div");
		УзелРазделаСтатуса.УстановитьАтрибут("class", "kanban-block");
		УзелРазделаСтатуса.УстановитьАтрибут("id", УникальныйИдентификаторСтатуса);
		УзелРазделаСтатуса.УстановитьАтрибут("fullNameObjectStatus", ДанныеСтатуса.СтатусЗадачи.Метаданные().ПолноеИмя());
		УзелКанбанТело.ДобавитьДочерний(УзелРазделаСтатуса);
		
		Для Каждого СтрокаЗадачи Из ЗадачиСоСтатусом Цикл
			
			ИД_Задачи = СтрШаблон("task%1", СтрокаЗадачи.ИдентификаторЗадача);
			ИД_Проекта = СтрШаблон("project%1", СтрокаЗадачи.ИдентификаторПроекта);
			ИД_Пользователя = ?(
				ПустаяСтрока(СтрокаЗадачи.ИдентификаторПользователя), 
				"", 
				СтрШаблон("user%1", СтрокаЗадачи.ИдентификаторПользователя));
			
			ИмяПользователяБезПробелов = СтрЗаменить(СтрокаЗадачи.ПредставлениеПользователя, " ", "_");
			Имя_Пользователя = ?(
					ПустаяСтрока(СтрокаЗадачи.ПредставлениеПользователя), 
					"", 
					СтрШаблон("user_name%1", ИмяПользователяБезПробелов));
			
			// Карточки
			УзелКарточка = ДокументHTML.СоздатьЭлемент("div");
			УзелКарточка.УстановитьАтрибут(
				"class", 
				СтрШаблон("card %1 %2 %3", ИД_Проекта, ИД_Пользователя, Имя_Пользователя));
			
			УзелКарточка.УстановитьАтрибут("draggable", "true");
			УзелКарточка.УстановитьАтрибут("id", ИД_Задачи);
			УзелКарточка.УстановитьАтрибут("fullNameObjectTask", СтрокаЗадачи.Задача.Метаданные().ПолноеИмя());
			
			#Область ШапкаКарточки
			
			УзелШапкаКарточки = ДокументHTML.СоздатьЭлемент("div");
			УзелШапкаКарточки.УстановитьАтрибут("class", "card__header");
			
			УзелТэгКарточки = ДокументHTML.СоздатьЭлемент("div");
			УзелТэгКарточки.УстановитьАтрибут("class", "tag_task");
			Если Не ПустаяСтрока(СтрокаЗадачи.ЦветПроекта) Тогда
				УзелТэгКарточки.УстановитьАтрибут("style", СтрШаблон("background-color: rgb(%1);", СтрокаЗадачи.ЦветПроекта));
			КонецЕсли;
			
			УзелШапкаКарточки.ДобавитьДочерний(УзелТэгКарточки);
			
			УзелШапкаКарточкиСсылка = ДокументHTML.СоздатьЭлемент("a");
			УзелШапкаКарточкиСсылка.УстановитьАтрибут("class", "card__link");
			УзелШапкаКарточкиСсылка.УстановитьАтрибут("href", СтрокаЗадачи.НавигационнаяСсылка);
			УзелШапкаКарточкиСсылка.ТекстовоеСодержимое = 
				СтрШаблон("%1 №%2", СтрокаЗадачи.Задача.Метаданные().ПредставлениеОбъекта, СтрокаЗадачи.Код);
			
			УзелШапкаКарточки.ДобавитьДочерний(УзелШапкаКарточкиСсылка);
			
			Если ПоказыватьФотоПользователя Тогда
				
				УзелШапкаКарточкиИзображение = ДокументHTML.СоздатьЭлемент("img");
				УзелШапкаКарточкиИзображение.УстановитьАтрибут("class", "card__photo");
				УзелШапкаКарточкиИзображение.УстановитьАтрибут("alt", СтрокаЗадачи.ПредставлениеПользователя);
				УзелШапкаКарточкиИзображение.УстановитьАтрибут("src", СтрокаЗадачи.ФотографияИсполнителя);
				
				УзелШапкаКарточки.ДобавитьДочерний(УзелШапкаКарточкиИзображение);
				
			КонецЕсли;
			
			УзелКарточка.ДобавитьДочерний(УзелШапкаКарточки);
			
			#КонецОбласти
			
			#Область ТелоКарточки
			
			УзелТелоКарточки = ДокументHTML.СоздатьЭлемент("div");
			УзелТелоКарточки.УстановитьАтрибут("class", "card__text");
			
			УзелТелоКарточкиТекст = ДокументHTML.СоздатьЭлемент("span");
			УзелТелоКарточкиТекст.ТекстовоеСодержимое = СтрокаЗадачи.Наименование;
			
			УзелТелоКарточки.ДобавитьДочерний(УзелТелоКарточкиТекст);
			УзелКарточка.ДобавитьДочерний(УзелТелоКарточки);
			
			#КонецОбласти
			
			УзелРазделаСтатуса.ДобавитьДочерний(УзелКарточка);
			
		КонецЦикла;
	
	КонецЦикла;
	
	УзелЗакрепленнаяШапка.ДобавитьДочерний(УзелКанбанХедер);
	УзелДоска.ДобавитьДочерний(УзелКанбанТело);
	
	ТекстКанбанДоска = канбан_КанбанДоска.ТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
КонецПроцедуры

// Заполнить данные задачи для ответа.
// 
// Параметры:
//  ЗадачаСсылка - ОпределяемыйТип.канбан_Задачи, ОпределяемыйТип.канбан_Ошибки, ПланВидовРасчетаСсылкаИмяПланаВидовРасчета, ПланСчетовСсылкаИмяПланаСчетов, ПланВидовХарактеристикСсылкаИмяПланаВидовХарактеристик, ДокументСсылкаИмяДокумента, ПланОбменаСсылкаИмяПланаОбмена, СправочникСсылкаИмяСправочника, БизнесПроцессСсылкаИмяБизнесПроцесса, ЗадачаСсылкаИмяЗадачи - Задача ссылка
//  ДанныеЗадачи - см. канбан_КанбанДоскаКлиент.НовыйДанныеЗадачиДляДоски
//
Процедура ЗаполнитьДанныеЗадачиДляОтвета(Знач ЗадачаСсылка, ДанныеЗадачи) Экспорт
	
	ИспользоватьСопоставлениеСтатусов = Константы.канбан_ИспользоватьСопоставлениеСтатусов.Получить();
	ТекстЗапросаДанныеЗадач = канбан_КанбанДоскаПереопределяемый.ТекстЗапросаДанныеЗадач();
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаДанныеЗадач);
	ПервыйПакет = СхемаЗапроса.ПакетЗапросов.Получить(СхемаЗапроса.ПакетЗапросов.Количество() -1);
	
	Если Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип.СодержитТип(ТипЗнч(ЗадачаСсылка)) Тогда
		
		ПервыйПакет.Операторы.Удалить(ПервыйПакет.Операторы.Количество() - 1);
		Оператор = ПервыйПакет.Операторы[0];
		Оператор.Отбор.Добавить("Задачи.Ссылка = &ЗадачаСсылка");
		
	ИначеЕсли ИспользоватьСопоставлениеСтатусов 
		И Метаданные.ОпределяемыеТипы.канбан_Ошибки.Тип.СодержитТип(ТипЗнч(ЗадачаСсылка)) Тогда
		
		ПервыйПакет.Операторы.Удалить(0);
		Оператор = ПервыйПакет.Операторы[0];
		Оператор.Отбор.Добавить("Ошибки.Ссылка = &ЗадачаСсылка");
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	ТексЗапросаИтоговый = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТексЗапросаИтоговый;
	Запрос.УстановитьПараметр("ЗадачаСсылка", ЗадачаСсылка);
	Выборка = Запрос.Выполнить();
	РезультатЗапроса = Выборка.Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		
		КартинкаПоУмолчанию = БиблиотекаКартинок.канбан_ФотоПоУмолчанию;
		ДвоичныеДанныеФотографииПоУмолчанию = КартинкаПоУмолчанию.ПолучитьДвоичныеДанные();
		
		СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(РезультатЗапроса.Задача);
		СсылкаНаИБ = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
		ДанныеЗадачи.card__link_href = СтрШаблон("%1#%2", СсылкаНаИБ, СсылкаНаОбъект);
		ДанныеЗадачи.card__link_name = 
			СтрШаблон("%1 №%2", 
				РезультатЗапроса.Задача.Метаданные().ПредставлениеОбъекта, 
				РезультатЗапроса.КодЗадачи);
		
		ДанныеЗадачи.card__photo = канбан_КанбанДоска.ЗаполнитьФотографиюИсполнителя(
			РезультатЗапроса.ХранилищеФотографии, ДвоичныеДанныеФотографииПоУмолчанию);
		ДанныеЗадачи.alt = РезультатЗапроса.ПредставлениеПользователя;
		ДанныеЗадачи.card__text = РезультатЗапроса.НаименованиеЗадачи;
		ДанныеЗадачи.fullnameobjecttask = РезультатЗапроса.Задача.Метаданные().ПолноеИмя();
		ДанныеЗадачи.idTask = СтрШаблон("task%1", Строка(РезультатЗапроса.Задача.УникальныйИдентификатор()));
		
		Если Метаданные.ОпределяемыеТипы.канбан_Ошибки.Тип.СодержитТип(ТипЗнч(ЗадачаСсылка)) Тогда
			
			Отказ = Ложь;
			ТипСтатусЗадачи = Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип.Типы()[0];
			СтатусЗадачи = Новый(ТипСтатусЗадачи);
			РегистрыСведений.канбан_СопоставлениеСтатусов.СопоставленныйСтатусЗадачи(
				СтатусЗадачи, РезультатЗапроса.СтатусЗадачи, Отказ);
			ДанныеЗадачи.status = Строка(СтатусЗадачи.УникальныйИдентификатор());
			
		Иначе
			
			ДанныеЗадачи.status = Строка(РезультатЗапроса.СтатусЗадачи.УникальныйИдентификатор());
			
		КонецЕсли;
		
		ДанныеЗадачи.project = СтрШаблон("project%1", Строка(РезультатЗапроса.УникальныйИдентификаторПроекта));
		ДанныеЗадачи.user = СтрШаблон("user%1", Строка(РезультатЗапроса.Исполнитель.УникальныйИдентификатор()));
		
		ИмяПользователяБезПробелов = СтрЗаменить(РезультатЗапроса.ПредставлениеПользователя, " ", "_");
		ДанныеЗадачи.user_name = СтрШаблон("user_name%1", ИмяПользователяБезПробелов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
