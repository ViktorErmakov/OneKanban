// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Текст запроса данные задач.
// 
// Возвращаемое значение:
//  Строка - Текст запроса данные задач
//  
//  Запрос должен вернуть поля:
// * Задача - ОпределяемыйТип.канбан_Задачи - ссылка на задачу
// * КодЗадачи - Число, Строка - код задачи
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач, ОпределяемыйТип.канбан_СтатусыОшибок - если используется сопоставление статусов.
// * ХранилищеФотографии - ХранилищеЗначения - фотография исполнителя задачи.
// * НаименованиеЗадачи - Строка - наименование задачи
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Исполнитель - ОпределяемыйТип.канбан_Пользователь
// * КодПроекта - Строка
// * НаименованиеПроекта - Строка
// * УникальныйИдентификаторПроекта - Строка
//
Функция ТекстЗапросаДанныеЗадач() Экспорт
	
	Запрос = Новый Запрос;
	Если Метаданные.Имя = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	Проекты.Ссылка КАК Проект,
			|	Задачи.Исполнитель КАК Исполнитель,
			|	Проекты.Код КАК КодПроекта,
			|	Проекты.Наименование КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	Справочник.ЗадачиПроцесса КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
			|			ПО ТехническиеПроекты.Владелец = Проекты.Ссылка
			|		ПО Задачи.Предмет = ТехническиеПроекты.Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Ошибки.Ссылка,
			|	Ошибки.Код,
			|	Ошибки.Статус,
			|	ЕСТЬNULL(Пользователи.Фотография, """"),
			|	Ошибки.Наименование,
			|	Ошибки.Владелец,
			|	Ошибки.КомуНаправлена,
			|	Проекты.Код,
			|	Проекты.Наименование,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка)
			|ИЗ
			|	Справочник.Ошибки КАК Ошибки
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Ошибки.КомуНаправлена = Пользователи.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
			|		ПО Ошибки.Владелец = Проекты.Ссылка";
			
		#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП_Tasks
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) КАК Проект,
			|	Задачи.Исполнитель КАК Исполнитель,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект.Код, Задачи.Проект.Код) КАК КодПроекта,
			|	Проекты.Наименование КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	Справочник.канбан_Задачи КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.канбан_Задачи КАК ЗадачиОсновные
			|		ПО Задачи.ОсновнаяЗадача = ЗадачиОсновные.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.канбан_Проекты КАК Проекты
			|		ПО ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) = Проекты.Ссылка
			|ГДЕ
			|	Задачи.ПоказыватьВОтчетахИКанбанДоске
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	канбан_Ошибки.Ссылка,
			|	канбан_Ошибки.Код,
			|	канбан_Ошибки.Статус,
			|	Пользователи.Фотография,
			|	канбан_Ошибки.Наименование,
			|	канбан_Ошибки.Владелец,
			|	канбан_Ошибки.Исполнитель,
			|	канбан_Проекты.Код,
			|	канбан_Проекты.Наименование,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(канбан_Проекты.Ссылка)
			|ИЗ
			|	Справочник.канбан_Ошибки КАК канбан_Ошибки
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО канбан_Ошибки.Исполнитель = Пользователи.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.канбан_Проекты КАК канбан_Проекты
			|		ПО канбан_Ошибки.Владелец = канбан_Проекты.Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	КодПроекта,
			|	КодЗадачи";
		
	#КонецОбласти
	
	КонецЕсли;
	
	Возврат Запрос.Текст;
	
КонецФункции

// Текст запроса статусы.
// Запрос должен вернуть заполненные поля:
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * Представление - Строка - представление статуса для канбан доски
// * УникальныйИдентификатор - Строка
// 
// Возвращаемое значение:
//  Строка - Текст запроса статусы
//
Функция ТекстЗапросаСтатусы() Экспорт
	
	Запрос = Новый Запрос;
	Если Метаданные.Имя = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		// Так как статусы в конфигурации есть перечисление, используем в качестве уникального идентификатора ПРЕДСТАВЛЕНИЕССЫЛКИ
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыЗадачи.Ссылка) КАК Представление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	Перечисление.СтатусыЦелевыхЗадач КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП_Tasks
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ВЫБОР
			|		КОГДА СтатусыЗадачи.НаименованиеДляКанбанДоски = """"
			|			ТОГДА СтатусыЗадачи.Наименование
			|		ИНАЧЕ СтатусыЗадачи.НаименованиеДляКанбанДоски
			|	КОНЕЦ КАК Представление,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	Справочник.канбан_СтатусыЗадач КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	КонецЕсли;
	
	Возврат Запрос.Текст;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  Задача - ОпределяемыйТип.канбан_Задачи
//  Статус - ОпределяемыйТип.канбан_СтатусыЗадач - устанавливаемый статус
//  Отказ - Булево
//
Процедура ИзменитьСтатусЗадаче(Знач Задача, Знач Статус, Отказ) Экспорт
	
	#Область Tasks_КонфигурацииНаБазеБСП
	
	Попытка
		
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		
		ЗадачаОбъект.Заблокировать();
		
		ЗадачаОбъект.Статус = Статус;
		
		ЗадачаОбъект.Записать();
		
	Исключение
		
		Отказ = Истина;
		
		ЗаписьЖурналаРегистрации(
			"ТипОперации", 
			УровеньЖурналаРегистрации.Ошибка,, 
			"Текст", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
	#КонецОбласти
	
КонецПроцедуры

// Статус задачи ссылка по идентификатору.
//
// Параметры:
//  ИдентификаторСтатусаСтрока - Строка
//  СтатусСсылка - ОпределяемыйТип.канбан_СтатусыЗадач
//
//@skip-check module-empty-method
Процедура СтатусЗадачиПоИдентификатору(Знач ИдентификаторСтатусаСтрока, СтатусСсылка) Экспорт
	
КонецПроцедуры


// Получает статус по идентификатору и менеджеру объекта.
//
// Параметры:
//   МенеджерОбъектаСтатусы - СправочникМенеджер, ПеречислениеМенеджер - Менеджер объекта, для которого получаются статусы
//   ИдентификаторСтатусаСтрока - Строка - Идентификатор статуса в строковом представлении
//   СтатусСсылка - ОпределяемыйТип.канбан_СтатусыЗадач - Ссылка на статус задачи
//   СтатусОшибка - ОпределяемыйТип.канбан_СтатусыОшибок - Ссылка на статус ошибки.
//
//@skip-check module-empty-method
Процедура СтатусПоИдентификаторуИМенеджеруОбъекта(
	Знач МенеджерОбъектаСтатусы, Знач ИдентификаторСтатусаСтрока, СтатусСсылка, СтатусОшибка) Экспорт
	
КонецПроцедуры

#КонецОбласти