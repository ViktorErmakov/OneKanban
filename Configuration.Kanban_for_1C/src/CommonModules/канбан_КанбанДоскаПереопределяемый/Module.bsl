// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Текст запроса данные задач.
// 
// Возвращаемое значение:
//  Строка - Текст запроса данные задач
//  
//  Запрос должен вернуть поля:
// * Задача - ОпределяемыйТип.канбан_Задачи - ссылка на задачу
// * КодЗадачи - Число, Строка - код задачи
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач, ОпределяемыйТип.канбан_СтатусыОшибок - если используется сопоставление статусов.
// * ХранилищеФотографии - ХранилищеЗначения - фотография исполнителя задачи.
// * ПредставлениеПользователя - Строка
// * НаименованиеЗадачи - Строка - наименование задачи
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Исполнитель - ОпределяемыйТип.канбан_Пользователь
// * КодПроекта - Строка
// * НаименованиеПроекта - Строка
// * УникальныйИдентификаторПроекта - Строка
//
Функция ТекстЗапросаДанныеЗадач() Экспорт
	
	ТекстОбъединенияЗапросов = канбан_КанбанДоска.ТекстОбъединитьВсе();
	ИспользоватьСопоставлениеСтатусов = Константы.канбан_ИспользоватьСопоставлениеСтатусов.Получить();
	ИмяКонфигурации = Метаданные.Имя;
	
	Если ИмяКонфигурации = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	ЕСТЬNULL(Пользователи.Представление, """") КАК ПредставлениеПользователя,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	Проекты.Ссылка КАК Проект,
			|	Задачи.Исполнитель КАК Исполнитель,
			|	Проекты.Код КАК КодПроекта,
			|	Проекты.Наименование КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	Справочник.ЗадачиПроцесса КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
			|			ПО ТехническиеПроекты.Владелец = Проекты.Ссылка
			|		ПО Задачи.Предмет = ТехническиеПроекты.Ссылка";
		
		Если ИспользоватьСопоставлениеСтатусов Тогда
			
			ТекстЗапросаОшибки =
				"ВЫБРАТЬ
				|	Ошибки.Ссылка КАК Ссылка,
				|	Ошибки.Код КАК Код,
				|	Ошибки.Статус КАК Статус,
				|	ЕСТЬNULL(Пользователи.Фотография, """") КАК Поле1,
				|	ЕСТЬNULL(Пользователи.Представление, """") КАК Поле2,
				|	Ошибки.Наименование КАК Наименование,
				|	Ошибки.Владелец КАК Владелец,
				|	Ошибки.КомуНаправлена КАК КомуНаправлена,
				|	Проекты.Код КАК Код1,
				|	Проекты.Наименование КАК Наименование1,
				|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК Поле3
				|ИЗ
				|	Справочник.Ошибки КАК Ошибки
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
				|		ПО Ошибки.КомуНаправлена = Пользователи.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
				|		ПО Ошибки.Владелец = Проекты.Ссылка";
			
			ТекстЗапроса = СтрШаблон("%1%2%3", ТекстЗапроса, ТекстОбъединенияЗапросов, ТекстЗапросаОшибки);
			
		КонецЕсли;
			
		#КонецОбласти
		
	ИначеЕсли ИмяКонфигурации = "УправлениеЗадачами" Тогда
		
		#Область Tasks
		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	ЕСТЬNULL(Пользователи.Представление, """") КАК ПредставлениеПользователя,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) КАК Проект,
			|	Задачи.Исполнитель КАК Исполнитель,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект.Код, Задачи.Проект.Код) КАК КодПроекта,
			|	Проекты.Наименование КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	Справочник.узЗадачи КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.узЗадачи КАК ЗадачиОсновные
			|		ПО Задачи.ОсновнаяЗадача = ЗадачиОсновные.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.узПроекты КАК Проекты
			|		ПО ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) = Проекты.Ссылка
			|ГДЕ
			|	Задачи.ПоказыватьВОтчетахИКанбанДоске";
		
		Если ИспользоватьСопоставлениеСтатусов Тогда
			
			ТекстЗапросаОшибки =
				"ВЫБРАТЬ
				|	Ошибки.Ссылка КАК Ссылка,
				|	Ошибки.Код КАК Код,
				|	Ошибки.Статус КАК Статус,
				|	ЕСТЬNULL(Пользователи.Фотография, """") КАК Поле1,
				|	ЕСТЬNULL(Пользователи.Представление, """") КАК Поле2,
				|	Ошибки.Наименование КАК Наименование,
				|	Ошибки.Проект КАК Проект,
				|	Ошибки.Исполнитель КАК Исполнитель,
				|	Проекты.Код КАК Код1,
				|	Проекты.Наименование КАК Наименование1,
				|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК Поле3
				|ИЗ
				|	Справочник.канбан_Ошибки КАК Ошибки
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
				|		ПО канбан_Ошибки.Исполнитель = Пользователи.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.узПроекты КАК Проекты
				|		ПО канбан_Ошибки.Проект = Проекты.Ссылка";
			
			ТекстЗапроса = СтрШаблон("%1%2%3", ТекстЗапроса, ТекстОбъединенияЗапросов, ТекстЗапросаОшибки);
			
		КонецЕсли;
		
	#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП
		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	ЕСТЬNULL(Пользователи.Представление, """") КАК ПредставлениеПользователя,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) КАК Проект,
			|	Задачи.Исполнитель КАК Исполнитель,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект.Код, Задачи.Проект.Код) КАК КодПроекта,
			|	Проекты.Наименование КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	Справочник.канбан_Задачи КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.канбан_Задачи КАК ЗадачиОсновные
			|		ПО Задачи.ОсновнаяЗадача = ЗадачиОсновные.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.канбан_Проекты КАК Проекты
			|		ПО ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) = Проекты.Ссылка
			|ГДЕ
			|	Задачи.ПоказыватьВОтчетахИКанбанДоске";
			
		Если ИспользоватьСопоставлениеСтатусов Тогда
			
			ТекстЗапросаОшибки =
				"ВЫБРАТЬ
				|	Ошибки.Ссылка КАК Ссылка,
				|	Ошибки.Код КАК Код,
				|	Ошибки.Статус КАК Статус,
				|	ЕСТЬNULL(Пользователи.Фотография, """") КАК Поле1,
				|	ЕСТЬNULL(Пользователи.Представление, """") КАК Поле2,
				|	Ошибки.Наименование КАК Наименование,
				|	Ошибки.Проект КАК Проект,
				|	Ошибки.Исполнитель КАК Исполнитель,
				|	Проекты.Код КАК Код1,
				|	Проекты.Наименование КАК Наименование1,
				|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК Поле3
				|ИЗ
				|	Справочник.канбан_Ошибки КАК Ошибки
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
				|		ПО Ошибки.Исполнитель = Пользователи.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.канбан_Проекты КАК Проекты
				|		ПО Ошибки.Проект = Проекты.Ссылка";
			
			ТекстЗапроса = СтрШаблон("%1%2%3", ТекстЗапроса, ТекстОбъединенияЗапросов, ТекстЗапросаОшибки);
			
		КонецЕсли;
		
	#КонецОбласти
	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Текст запроса статусы.
// Запрос должен вернуть заполненные поля:
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * Представление - Строка - представление статуса для канбан доски
// * УникальныйИдентификатор - Строка
// 
// Возвращаемое значение:
//  Строка - Текст запроса статусы
//
Функция ТекстЗапросаСтатусы() Экспорт
	
	Запрос = Новый Запрос;
	Если Метаданные.Имя = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		// Так как статусы в конфигурации есть перечисление, используем в качестве уникального идентификатора ПРЕДСТАВЛЕНИЕССЫЛКИ
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыЗадачи.Ссылка) КАК Представление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	Перечисление.СтатусыЗадачПроцессов КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	ИначеЕсли Метаданные.Имя = "УправлениеЗадачами" Тогда
		
		#Область Tasks
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ВЫБОР
			|		КОГДА СтатусыЗадачи.НаименованиеДляКанбанДоски = """"
			|			ТОГДА СтатусыЗадачи.Наименование
			|		ИНАЧЕ СтатусыЗадачи.НаименованиеДляКанбанДоски
			|	КОНЕЦ КАК Представление,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	Справочник.узСтатусыЗадачи КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ВЫБОР
			|		КОГДА СтатусыЗадачи.НаименованиеДляКанбанДоски = """"
			|			ТОГДА СтатусыЗадачи.Наименование
			|		ИНАЧЕ СтатусыЗадачи.НаименованиеДляКанбанДоски
			|	КОНЕЦ КАК Представление,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	Справочник.канбан_СтатусыЗадач КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	КонецЕсли;
	
	Возврат Запрос.Текст;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  Задача - ОпределяемыйТип.канбан_Задачи
//  Статус - ОпределяемыйТип.канбан_СтатусыЗадач - устанавливаемый статус
//  Исполнитель - ОпределяемыйТип.канбан_Пользователь
//
Процедура ИзменитьСтатусЗадаче(Знач Задача, Знач Статус, Знач Исполнитель) Экспорт
	
	#Область Tasks_КонфигурацииНаБазеБСП_СистемаПроектированияПрикладныхРешений
	
	Попытка
		
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		
		ЗадачаОбъект.Заблокировать();
		
		Если ЗадачаОбъект.Статус <> Статус Тогда
			ЗадачаОбъект.Статус = Статус;
		КонецЕсли;
		
		Если ЗадачаОбъект.Исполнитель <> Исполнитель Тогда
			ЗадачаОбъект.Исполнитель = Исполнитель;
		КонецЕсли;
		
		Если ЗадачаОбъект.Модифицированность() Тогда
			ЗадачаОбъект.Записать();
		КонецЕсли;
		
	Исключение
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить объект %1, объект может быть занят другим пользователем. 
			|Продробнее см. в журнале регистрации'"), Задача);
		ТипОперации = НСтр("ru = 'OneKanban: ИзменитьСтатусЗадаче'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Задача);
		
		ЗаписьЖурналаРегистрации(
			ТипОперации, 
			УровеньЖурналаРегистрации.Ошибка, , 
			ТекстСообщения, 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	#КонецОбласти
	
КонецПроцедуры

// Статус задачи ссылка по идентификатору.
//
// Параметры:
//  ИмяОпределяемогоТипа - Строка
//  ИдентификаторСтатусаСтрока - Строка
//  СтатусСсылка - ОпределяемыйТип.канбан_СтатусыЗадач, ОпределяемыйТип.канбан_СтатусыОшибок - статус ссылка.
//
//@skip-check module-empty-method
Процедура СтатусОбъектаПоИдентификатору(Знач ИмяОпределяемогоТипа, Знач ИдентификаторСтатусаСтрока, СтатусСсылка) Экспорт
	
КонецПроцедуры

#КонецОбласти