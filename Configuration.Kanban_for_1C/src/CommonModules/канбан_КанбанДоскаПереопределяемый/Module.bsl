// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Текст запроса данные задач.
// 
// Возвращаемое значение:
//  Строка - Текст запроса данные задач
//  
//  Запрос должен вернуть поля:
// * Задача - ОпределяемыйТип.канбан_Задачи - ссылка на задачу
// * КодЗадачи - Число, Строка - код задачи
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
// * ХранилищеФотографии - ХранилищеЗначения - фотография исполнителя задачи.
// * НаименованиеЗадачи - Строка - наименование задачи
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Исполнитель - ОпределяемыйТип.канбан_Пользователь
// * КодПроекта - Строка
// * НаименованиеПроекта - Строка
// * УникальныйИдентификаторПроекта - Строка
//
Функция ТекстЗапросаДанныеЗадач() Экспорт
	
	Запрос = Новый Запрос;
	Если Метаданные.Имя = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	ЕСТЬNULL(Проекты.Ссылка, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК Проект,
			|	ЕСТЬNULL(Задачи.Исполнитель, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Исполнитель,
			|	ЕСТЬNULL(Проекты.Код, """") КАК КодПроекта,
			|	ЕСТЬNULL(Проекты.Наименование, """") КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ПОМЕСТИТЬ Объединение
			|ИЗ
			|	Справочник.ЗадачиПроцесса КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты КАК ТехническиеПроекты
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
			|			ПО ТехническиеПроекты.Владелец = Проекты.Ссылка
			|		ПО Задачи.Предмет = ТехническиеПроекты.Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Ошибки.Ссылка,
			|	Ошибки.Код,
			|	Ошибки.Статус,
			|	ЕСТЬNULL(Пользователи.Фотография, """"),
			|	Ошибки.Наименование,
			|	ЕСТЬNULL(Ошибки.Владелец.Ссылка, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)),
			|	ЕСТЬNULL(Ошибки.КомуНаправлена, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)),
			|	ЕСТЬNULL(Проекты.Код, """"),
			|	ЕСТЬNULL(Проекты.Наименование, """"),
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка)
			|ИЗ
			|	Справочник.Ошибки КАК Ошибки
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Ошибки.КомуНаправлена = Пользователи.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
			|		ПО Ошибки.Владелец = Проекты.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Объединение.Задача КАК Задача,
			|	Объединение.КодЗадачи КАК КодЗадачи,
			|	Объединение.СтатусЗадачи КАК СтатусЗадачи,
			|	Объединение.ХранилищеФотографии КАК ХранилищеФотографии,
			|	Объединение.НаименованиеЗадачи КАК НаименованиеЗадачи,
			|	Объединение.Проект КАК Проект,
			|	Объединение.Исполнитель КАК Исполнитель,
			|	Объединение.КодПроекта КАК КодПроекта,
			|	Объединение.НаименованиеПроекта КАК НаименованиеПроекта,
			|	Объединение.УникальныйИдентификаторПроекта КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	Объединение КАК Объединение";
			
		#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП_Tasks
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Задачи.Ссылка КАК Задача,
			|	Задачи.Код КАК КодЗадачи,
			|	Задачи.Статус КАК СтатусЗадачи,
			|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
			|	Задачи.Наименование КАК НаименованиеЗадачи,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) КАК Проект,
			|	Задачи.Исполнитель КАК Исполнитель,
			|	ЕСТЬNULL(ЗадачиОсновные.Проект.Код, Задачи.Проект.Код) КАК КодПроекта,
			|	Проекты.Наименование КАК НаименованиеПроекта,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Проекты.Ссылка) КАК УникальныйИдентификаторПроекта
			|ИЗ
			|	&ОбъектЗапроса КАК Задачи
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО Задачи.Исполнитель = Пользователи.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ &ОбъектЗапроса КАК ЗадачиОсновные
			|		ПО Задачи.ОсновнаяЗадача = ЗадачиОсновные.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ &ОбъектПроекты КАК Проекты
			|		ПО ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) = Проекты.Ссылка
			|ГДЕ
			|	Задачи.ПоказыватьВОтчетахИКанбанДоске
			|
			|УПОРЯДОЧИТЬ ПО
			|	КодПроекта,
			|	КодЗадачи";
		
	#КонецОбласти
	
	КонецЕсли;
	
	канбан_КанбанДоска.УстановитьОбъектЗапроса(Запрос.Текст, Метаданные.ОпределяемыеТипы.канбан_Задачи.Имя, "&ОбъектЗапроса");
	канбан_КанбанДоска.УстановитьОбъектЗапроса(Запрос.Текст, Метаданные.ОпределяемыеТипы.канбан_Проекты.Имя, "&ОбъектПроекты");
	
	Возврат Запрос.Текст;
	
КонецФункции

// Текст запроса статусы.
// Запрос должен вернуть заполненные поля:
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * Представление - Строка - представление статуса для канбан доски
// * УникальныйИдентификатор - Строка
// 
// Возвращаемое значение:
//  Строка - Текст запроса статусы
//
Функция ТекстЗапросаСтатусы() Экспорт
	
	Запрос = Новый Запрос;
	Если Метаданные.Имя = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		// Так как статусы в конфигурации есть перечисление, используем в качестве уникального идентификатора ПРЕДСТАВЛЕНИЕССЫЛКИ
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыЗадачи.Ссылка) КАК Представление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	&ОбъектЗапроса КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП_Tasks
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтатусыЗадачи.Ссылка КАК Статус,
			|	ВЫБОР
			|		КОГДА СтатусыЗадачи.НаименованиеДляКанбанДоски = """"
			|			ТОГДА СтатусыЗадачи.Наименование
			|		ИНАЧЕ СтатусыЗадачи.НаименованиеДляКанбанДоски
			|	КОНЕЦ КАК Представление,
			|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
			|ИЗ
			|	&ОбъектЗапроса КАК СтатусыЗадачи";
		
		#КонецОбласти
		
	КонецЕсли;
	
	канбан_КанбанДоска.УстановитьОбъектЗапроса(Запрос.Текст, Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Имя, "&ОбъектЗапроса");
	
	Возврат Запрос.Текст;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  Задача - ОпределяемыйТип.канбан_Задачи
//  Статус - ОпределяемыйТип.канбан_СтатусыЗадач - устанавливаемый статус
//  Отказ - Булево
//
Процедура ИзменитьСтатусЗадаче(Знач Задача, Знач Статус, Отказ) Экспорт
	
	#Область Tasks_КонфигурацииНаБазеБСП
	
	Попытка
		
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		
		ЗадачаОбъект.Заблокировать();
		
		ЗадачаОбъект.Статус = Статус;
		
		ЗадачаОбъект.Записать();
		
	Исключение
		
		Отказ = Истина;
		
		ЗаписьЖурналаРегистрации(
			"ТипОперации", 
			УровеньЖурналаРегистрации.Ошибка,, 
			"Текст", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
	#КонецОбласти
	
КонецПроцедуры

// Статус задачи ссылка по идентификатору.
//
// Параметры:
//  ИдентификаторСтатусаСтрока - Строка
//  СтатусСсылка - ОпределяемыйТип.канбан_СтатусыЗадач
//
Функция СтатусЗадачиПоИдентификатору(Знач ИдентификаторСтатусаСтрока, СтатусСсылка) Экспорт
	
	Если Метаданные.Имя = "СистемаПроектированияПрикладныхРешений" Тогда
		
		#Область СистемаПроектированияПрикладныхРешений
		
		СтатусСсылка = Перечисления.СтатусыЗадачПроцессов[ИдентификаторСтатусаСтрока];
		Возврат СтатусСсылка;
		
		#КонецОбласти
		
	Иначе
		
		#Область КонфигурацииНаБазеБСП_Tasks
		
		МенеджерОбъектаСтатусы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СтатусСсылка);
		ИдентификаторСтатуса = Новый УникальныйИдентификатор(ИдентификаторСтатусаСтрока);
		СтатусСсылка = МенеджерОбъектаСтатусы.ПолучитьСсылку(ИдентификаторСтатуса);
		
		#КонецОбласти
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти