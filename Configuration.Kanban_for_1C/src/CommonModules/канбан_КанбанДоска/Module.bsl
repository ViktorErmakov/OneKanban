// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Получить текст HTML.
// 
// Параметры:
//  КанбанДоска - Строка - HTML страница доски.
//  УникальныйИдентификатор - УникальныйИдентификатор, Строка - гуид формы или адрес во временном хранилище.
//  ПоказыватьФотоПользователя - Булево - Показывать фото пользователя
// 
Процедура ТекстHTMLКанбанДоски(
		КанбанДоска, УникальныйИдентификатор = "", ПоказыватьФотоПользователя = Истина) Экспорт
	
	Обработки.Канбан_Доска_HTML.ТекстHTMLКанбанДоски(
		КанбанДоска, УникальныйИдентификатор, ПоказыватьФотоПользователя);
	
КонецПроцедуры

// Текст HTMLРедактора.
// 
// Параметры:
//  СтраницаHTMLРедактора - Строка - HTML страница редактора
//  УникальныйИдентификатор - УникальныйИдентификатор, Строка - гуид формы или адрес во временном хранилище.
//
Процедура ТекстHTMLРедактора(СтраницаHTMLРедактора, УникальныйИдентификатор = "") Экспорт
	
	Справочники.канбан_Задачи.ТекстHTMLРедактора(СтраницаHTMLРедактора, УникальныйИдентификатор);
	
КонецПроцедуры

// Используемые проекты доски сохранить.
// 
// Параметры:
//  ИспользуемыеПроектыДоски - см. НовыйПроектыДоски
//  Отказ - Булево
//
Процедура ИспользуемыеПроектыДоскиСохранить(ИспользуемыеПроектыДоски, Отказ = Истина) Экспорт
	
	РегистрыСведений.канбан_ИспользуемыеПроектыДоски.Записать(ИспользуемыеПроектыДоски, Отказ);
	
КонецПроцедуры

// Используемые проекты доски загрузить.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыйПроектыДоски
//
Функция ИспользуемыеПроектыДоскиЗагрузить() Экспорт
	
	ПроектыДоски = НовыйПроектыДоски();
	
	РегистрыСведений.канбан_ИспользуемыеПроектыДоски.Загрузить(ПроектыДоски);
	
	Возврат ПроектыДоски;
	
КонецФункции

// Используемые статусы доски сохранить.
// 
// Параметры:
//  ИспользуемыеСтатусыДоски - см. НовыйСтатусыДоски
//  Отказ - Булево
//
Процедура ИспользуемыеСтатусыДоскиСохранить(ИспользуемыеСтатусыДоски, Отказ = Истина) Экспорт
	
	РегистрыСведений.канбан_ИспользуемыеСтатусыДоски.Записать(ИспользуемыеСтатусыДоски, Отказ);
	
КонецПроцедуры

// Используемые статусы доски сохраненные для текущего пользователя
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыйСтатусыДоски
//
Функция ИспользуемыеСтатусыДоскиЗагрузить() Экспорт
	
	СтатусыДоски = НовыйСтатусыДоски();
	
	РегистрыСведений.канбан_ИспользуемыеСтатусыДоски.Загрузить(СтатусыДоски);
	
	Возврат СтатусыДоски;
	
КонецФункции

// Получает ссылку на статус задачи
//
// Параметры:
//  ИдентификаторСтатусаСтрока	 - Строка
// 
// Возвращаемое значение:
//   - ОпределяемыйТип.канбан_СтатусыЗадач
//
Функция СтатусЗадачиПоИдентификатору(Знач ИдентификаторСтатусаСтрока) Экспорт
	
	ТипОбъектаСтатус = Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип.Типы()[0];
	СтатусСсылка = Новый (ТипОбъектаСтатус); // ОпределяемыйТип.канбан_СтатусыЗадач
	
	ИдентификаторСтатусЗадачи = Новый УникальныйИдентификатор(ИдентификаторСтатусаСтрока);
	МенеджерЗадач = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СтатусСсылка);
	
	СтатусСсылка = МенеджерЗадач.ПолучитьСсылку(ИдентификаторСтатусЗадачи); // ОпределяемыйТип.канбан_СтатусыЗадач
	
	канбан_КанбанДоскаПереопределяемый.СтатусЗадачиПоИдентификатору(ИдентификаторСтатусаСтрока, СтатусСсылка);
	
	Возврат СтатусСсылка;
	
КонецФункции

// Получает ссылку на статус ошибок
//
// Параметры:
//  ИдентификаторСтатусаСтрока	 - Строка
// 
// Возвращаемое значение:
//   - ОпределяемыйТип.канбан_СтатусыОшибок
//
Функция СтатусОшибкиПоИдентификатору(Знач ИдентификаторСтатусаСтрока) Экспорт
	
	ТипОбъектаСтатус = Метаданные.ОпределяемыеТипы.канбан_СтатусыОшибок.Тип.Типы()[0];
	СтатусСсылка = Новый (ТипОбъектаСтатус); // ОпределяемыйТип.канбан_СтатусыОшибок
	
	ИдентификаторСтатусЗадачи = Новый УникальныйИдентификатор(ИдентификаторСтатусаСтрока);
	МенеджерЗадач = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СтатусСсылка);
	СтатусСсылка = МенеджерЗадач.ПолучитьСсылку(ИдентификаторСтатусЗадачи); // ОпределяемыйТип.канбан_СтатусыОшибок
	
	канбан_КанбанДоскаПереопределяемый.СтатусОшибкиПоИдентификатору(ИдентификаторСтатусаСтрока, СтатусСсылка);
	
	Возврат СтатусСсылка;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  ДанныеПриИзмененииСтатуса - см. канбан_КанбанДоскаКлиент.НовыйДанныеПриИзмененииСтатуса
//  Отказ - Булево
//
// Возвращаемое значение:
//  ОпределяемыйТип.канбан_Задачи
//
Функция ИзменитьСтатусЗадаче(ДанныеПриИзмененииСтатуса, Отказ) Экспорт
	
	ЗадачаСсылка = ЗадачаСсылкаПоИдентификатору(ДанныеПриИзмененииСтатуса.ИдентификаторЗадача, ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаЗадача);
	
	СтатусСсылка = НовыйСтатусПоИдентификаторуИМенеджеруОбъекта(
		ЗадачаСсылка, ДанныеПриИзмененииСтатуса.ИдентификаторСтатуса, ДанныеПриИзмененииСтатуса.ИмяМенеджераОбъектаСтатус);
	
	канбан_КанбанДоскаПереопределяемый.ИзменитьСтатусЗадаче(ЗадачаСсылка, СтатусСсылка, Отказ);
	
	Возврат ЗадачаСсылка;
	
КонецФункции

// Данные для канбан доски.
// 
// Возвращаемое значение:
//  см. НовыйДанныеДляКанбанДоски
//
Функция ДанныеДляКанбанДоски() Экспорт
	
	ТекстЗапросаДанныеЗадач = канбан_КанбанДоскаПереопределяемый.ТекстЗапросаДанныеЗадач();
	
	ДанныеДляКанбанДоски = НовыйДанныеДляКанбанДоски();
	
	ДанныеДляКанбанДоски.Проекты = ИспользуемыеПроектыДоскиЗагрузить();
	
	ПроектыДоски = ДанныеДляКанбанДоски.Проекты;
	Если ПроектыДоски.Количество() = 0 Тогда
		
		ЗаполнитьВсемиПроектами(ТекстЗапросаДанныеЗадач, ПроектыДоски);
		
	КонецЕсли;
	
	ПараметрыОтбора = НовыйПараметрыОтбораЗадач();
	
	ПараметрыОтбора.Проекты = ПроектыДоски.ВыгрузитьКолонку("Проект"); // Массив из ОпределяемыйТип.канбан_Проекты
	
	ТекстЗапросаСтатусы = канбан_КанбанДоскаПереопределяемый.ТекстЗапросаСтатусы();
	РегистрыСведений.канбан_ИспользуемыеСтатусыДоски.Статусы(ТекстЗапросаСтатусы, ДанныеДляКанбанДоски);
	
	ДополнитьЗапросДанныеЗадач(ТекстЗапросаДанныеЗадач);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеЗадач;
	
	Запрос.УстановитьПараметр("Проекты", ПараметрыОтбора.Проекты);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыОтбора.ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	КартинкаПоУмолчанию = БиблиотекаКартинок.канбан_ФотоПоУмолчанию;
	ДвоичныеДанныеФотографииПоУмолчанию = КартинкаПоУмолчанию.ПолучитьДвоичныеДанные();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДанныеДляКанбанДоски.ЗадачиИСтатусы.Добавить();
		НоваяСтрока.Задача = Выборка.Задача;
		НоваяСтрока.Статус = Выборка.СтатусЗадачи;
		
		ХранилищеФотографии = Выборка.ХранилищеФотографии;
		Если ПустаяСтрока(ХранилищеФотографии) 
			Или ХранилищеФотографии.Получить() = Неопределено Тогда
			
			ДвоичныеДанныеФотографии = ДвоичныеДанныеФотографииПоУмолчанию;
			
		Иначе
			
			ДвоичныеДанныеФотографии = ХранилищеФотографии.Получить();
			
		КонецЕсли;
		
		ФотографияСтрокаBase64 = Base64Строка(ДвоичныеДанныеФотографии);
		НоваяСтрока.ФотографияИсполнителя = СтрШаблон("data:image/jpeg;base64,%1", ФотографияСтрокаBase64);
		
		НоваяСтрока.Наименование = Выборка.НаименованиеЗадачи;
		
		СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(Выборка.Задача);
		СсылкаНаИБ = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
		НоваяСтрока.НавигационнаяСсылка = СтрШаблон("%1#%2", СсылкаНаИБ, СсылкаНаОбъект);
		
		НоваяСтрока.ИдентификаторЗадача = Строка(Выборка.Задача.УникальныйИдентификатор());
		НоваяСтрока.Код = Выборка.КодЗадачи;
		НоваяСтрока.Проект = Выборка.Проект;
		НоваяСтрока.ПредставлениеПроекта = Строка(Выборка.Проект);
		НоваяСтрока.ЦветПроекта = Выборка.ЦветПроекта;
		НоваяСтрока.ИдентификаторПроекта = Строка(Выборка.УникальныйИдентификаторПроекта);
		НоваяСтрока.ИдентификаторПользователя = 
			?(Выборка.Исполнитель.Пустая(), "", Строка(Выборка.Исполнитель.УникальныйИдентификатор()));
		
	КонецЦикла;
	
	Возврат ДанныеДляКанбанДоски;
	
КонецФункции

// Получает текст HTML из объекта ДокументHTML.
//
// Параметры:
//  ДокументHTML  - ДокументHTML - документ, из которого будет извлекаться текст.
//
// Возвращаемое значение:
//   Строка   - текст HTML
//
Функция ТекстHTMLИзОбъектаДокументHTML(Знач ДокументHTML) Экспорт
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	
	Возврат ЗаписьHTML.Закрыть();
	
КонецФункции

// Получить объект документ HTMLИз текста HTML. // Заимствована из ОбщийМодуль.Взаимодействие:
// 
// Параметры:
//  ТекстHTML  - Строка
//  Кодировка - Строка
// 
// Возвращаемое значение:
//   ДокументHTML   - созданный документ HTML.
//
Функция ПолучитьОбъектДокументHTMLИзТекстаHTML(ТекстHTML, Кодировка = "") Экспорт
	
	Построитель = Новый ПостроительDOM;
	ЧтениеHTML = Новый ЧтениеHTML;
	
	Попытка
	
		ЧтениеHTML.УстановитьСтроку(ТекстHTML, Кодировка);
	
	Исключение
		
		ЧтениеHTML.УстановитьСтроку(ТекстHTML);
		
	КонецПопытки;
	
	Возврат Построитель.Прочитать(ЧтениеHTML);
	
КонецФункции

// Полное имя объекта метаданных определяемого типа.
// 
// Параметры:
//  ИмяОпределяемогоТипа - Строка
// 
// Возвращаемое значение:
//  Строка - Полное имя объекта метаданных определяемого типа
//
Функция ПолноеИмяОбъектаМетаданныхОпределяемогоТипа(Знач ИмяОпределяемогоТипа) Экспорт
	
	Тип = Метаданные.ОпределяемыеТипы[ИмяОпределяемогоТипа].Тип.Типы()[0];
	ТипСсылка = Новый (Тип); // СправочникСсылка
	МетаданныеТипа = ТипСсылка.Метаданные();
	
	ПолноеИмяОбъекта = МетаданныеТипа.ПолноеИмя();
	
	Возврат ПолноеИмяОбъекта; 
	
КонецФункции

// Загрузить приложение.
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные - Двоичные данные страницы
//  Результат - Строка - реквизит формы для отображения приложения.
//  УникальныйИдентификатор - УникальныйИдентификатор - Уникальный идентификатор формы.
// 
Процедура ЗагрузитьПриложение(ДвоичныеДанные, Результат, УникальныйИдентификатор) Экспорт
	
	Если КлиентскоеПриложение.ТипПриложения() = ТипКлиентскогоПриложения.ВебКлиент Тогда
		
		Результат = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанные);
		
	Иначе
		
		АдресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		Результат = СтрШаблон(
			"%1/%2", 
			ПолучитьНавигационнуюСсылкуИнформационнойБазы(), 
			АдресВХранилище);
		
	КонецЕсли;
	
КонецПроцедуры

// Преобразует строковое представление цвета в объект Цвет.
//
// Параметры:
//   ЦветСтрокой - Строка - Строковое представление цвета в формате "R,G,B"
//
// Возвращаемое значение:
//   Цвет - Объект Цвет, соответствующий указанному строковому представлению
//
Функция ЦветИзСтроки(ЦветСтрокой) Экспорт
	
	Если ПустаяСтрока(ЦветСтрокой) Тогда
		
		Цвет = Новый Цвет;
		
	Иначе
		
		Цвета = СтрРазделить(ЦветСтрокой, ",");
		//@skip-check new-color
		Цвет = Новый Цвет(Цвета[0], Цвета[1], Цвета[2]);
		
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

// Преобразует цвет в строковое представление.
//
// Параметры:
//   Цвет - Цвет - Цвет, который необходимо преобразовать в строку
//
// Возвращаемое значение:
//   Строка - Строковое представление цвета в формате "Красный, Зеленый, Синий"
//
Функция СтрокаИзЦвета(Цвет) Экспорт
	
	АбсолютныйЦвет = Цвет.ПолучитьАбсолютный();
	Результат = СтрШаблон(
		"%1, %2, %3", 
		Строка(АбсолютныйЦвет.Красный), 
		Строка(АбсолютныйЦвет.Зеленый), 
		Строка(АбсолютныйЦвет.Синий));
		
	Возврат Результат;
	
КонецФункции

// Получает сопоставленный статус
//
// Параметры:
//  СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
//  СтатусОшибки - ОпределяемыйТип.канбан_СтатусыОшибок
//
Процедура СопоставленныйСтатус(СтатусЗадачи, СтатусОшибки) Экспорт
	
	// TODO проверку добавить на входящие параметры
//	Условие = Истина;
//	Сообщение = "";
//	ОбщегоНазначенияКлиентСервер.Проверить(Условие, Сообщение, "канбан_КанбанДоскаю.СопоставленныйСтатус");
//	
//	Условие = Истина;
//	Сообщение = "";
//	ОбщегоНазначенияКлиентСервер.Проверить(Условие, Сообщение, "канбан_КанбанДоскаю.СопоставленныйСтатус");
	
	РегистрыСведений.канбан_СопоставлениеСтатусов.СопоставленныйСтатус(СтатусЗадачи, СтатусОшибки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Конструкторы

// Новый параметры отбора задач.
// 
// Возвращаемое значение:
//  Структура - Новый параметры отбора задач:
//  * Проекты - Массив из ОпределяемыйТип.канбан_Проекты
//  * ТекущийПользователь - ОпределяемыйТип.канбан_Пользователь
//
Функция НовыйПараметрыОтбораЗадач() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Проекты", Новый Массив);
	Результат.Вставить("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Возврат Результат;
	
КонецФункции

// Новый данные для канбан доски.
// 
// Возвращаемое значение:
//  Структура:
// * Статусы - см. НовыйСтатусыДоски
// * ЗадачиИСтатусы - см. НовыйЗадачиИСтатусы
// * Проекты - см. НовыйПроектыДоски
//
Функция НовыйДанныеДляКанбанДоски() Экспорт
	
	СтатусыДоски = НовыйСтатусыДоски();
	
	ЗадачиИСтатусы = НовыйЗадачиИСтатусы();
	
	ПроектыДоски = НовыйПроектыДоски();
	
	ДанныеДляКанбанДоски = Новый Структура;
	ДанныеДляКанбанДоски.Вставить("Статусы", СтатусыДоски);
	ДанныеДляКанбанДоски.Вставить("ЗадачиИСтатусы", ЗадачиИСтатусы);
	ДанныеДляКанбанДоски.Вставить("Проекты", ПроектыДоски);
	
	Возврат ДанныеДляКанбанДоски;
	
КонецФункции

// Новый статусы доски.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый статусы доски:
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
// * СтатусОшибки - ОпределяемыйТип.канбан_СтатусыОшибок
// * НомерПоПорядку - Число
// * КнопкаДобавить - Булево
// * Представление - Строка
// * УникальныйИдентификатор - Строка
//
Функция НовыйСтатусыДоски() Экспорт
	
	СтатусыДоски = Новый ТаблицаЗначений;
	СтатусыДоски.Колонки.Добавить("СтатусЗадачи", Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип);
	СтатусыДоски.Колонки.Добавить("СтатусОшибки", Метаданные.ОпределяемыеТипы.канбан_СтатусыОшибок.Тип);
	СтатусыДоски.Колонки.Добавить("НомерПоПорядку", Новый ОписаниеТипов("Число"));
	СтатусыДоски.Колонки.Добавить("КнопкаДобавить", Новый ОписаниеТипов("Булево"));
	СтатусыДоски.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	СтатусыДоски.Колонки.Добавить("УникальныйИдентификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат СтатусыДоски;
	
КонецФункции

// Новый задачи и статусы.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый задачи и статусы:
// * Задача - ОпределяемыйТип.канбан_Задачи, ОпределяемыйТип.канбан_Ошибки - Задачи или ошибки.
// * Наименование - Строка
// * Код - Строка
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * ФотографияИсполнителя - Строка
// * ИдентификаторПользователя - Строка
// * НавигационнаяСсылка - Строка
// * ИдентификаторЗадача - Строка
// * Проект - ОпределяемыйТип.канбан_Проекты
// * ПредставлениеПроекта - Строка
// * ИдентификаторПроекта - Строка
// * ЦветПроекта - Строка
//
Функция НовыйЗадачиИСтатусы()
	
	ЗадачиИСтатусы = Новый ТаблицаЗначений;
	
	МассивТипов1 = Новый Массив;
	МассивТипов1.Добавить(Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип.Типы()[0]);
	МассивТипов1.Добавить(Метаданные.ОпределяемыеТипы.канбан_Ошибки.Тип.Типы()[0]);
	ОписаниеТиповЗадача = Новый ОписаниеТипов(МассивТипов1);
	ЗадачиИСтатусы.Колонки.Добавить("Задача", ОписаниеТиповЗадача);
	
	ЗадачиИСтатусы.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("Статус", Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип);
	ЗадачиИСтатусы.Колонки.Добавить("ФотографияИсполнителя", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ИдентификаторПользователя", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("НавигационнаяСсылка", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ИдентификаторЗадача", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("Проект", Метаданные.ОпределяемыеТипы.канбан_Проекты.Тип);
	ЗадачиИСтатусы.Колонки.Добавить("ПредставлениеПроекта", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ИдентификаторПроекта", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ЦветПроекта", Новый ОписаниеТипов("Строка"));
	
	Возврат ЗадачиИСтатусы;
	
КонецФункции

// Новый проекты доски.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый проекты доски:
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Цвет - Цвет
// * ИдентификаторПроектаСтрока - Строка
// * ПредставлениеПроекта - Строка
//
Функция НовыйПроектыДоски() Экспорт
	
	ПроектыДоски = Новый ТаблицаЗначений();
	ПроектыДоски.Колонки.Добавить("Проект", Метаданные.ОпределяемыеТипы.канбан_Проекты.Тип);
	ПроектыДоски.Колонки.Добавить("Цвет", Новый ОписаниеТипов("Цвет"));
	ПроектыДоски.Колонки.Добавить("ИдентификаторПроектаСтрока", Новый ОписаниеТипов("Строка"));
	ПроектыДоски.Колонки.Добавить("ПредставлениеПроекта", Новый ОписаниеТипов("Строка"));
	
	Возврат ПроектыДоски;
	
КонецФункции

#КонецОбласти

//Функция КлючОбъекта()
//	Возврат "КанбанДоскаHTML";
//КонецФункции

// Дополнить запрос данные задач.
// 
// Параметры:
//  ТекстЗапросаДанныеЗадач - см. канбан_КанбанДоскаПереопределяемый.ТекстЗапросаДанныеЗадач
// 
Процедура ДополнитьЗапросДанныеЗадач(ТекстЗапросаДанныеЗадач)
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаДанныеЗадач);
	ПервыйПакет = СхемаЗапроса.ПакетЗапросов.Получить(СхемаЗапроса.ПакетЗапросов.Количество() -1);
	Оператор = ПервыйПакет.Операторы[0];
	
	#Область ДанныеСтатусов
	
	// Соединяем с таблицей используемых статусов, отсекаем лишние задачи.
	ИспользуемыеСтатусыДоски = Оператор.Источники.Добавить("РегистрСведений.канбан_ИспользуемыеСтатусыДоски", "ИспользуемыеСтатусыДоски");
	ИспользуемыеСтатусыДоски.Соединения.Очистить();
	
	УсловиеСоединенияСтатусыДоски = СтрШаблон(
		"%1 = ИспользуемыеСтатусыДоски.СтатусЗадачи И ИспользуемыеСтатусыДоски.Пользователь = &ТекущийПользователь", 
		Оператор.ВыбираемыеПоля[2]);
	
	Оператор.Источники[0].Соединения.Добавить(ИспользуемыеСтатусыДоски, УсловиеСоединенияСтатусыДоски);
	СоединениеИспользуемыеСтатусыДоски = Оператор.Источники[0].Соединения.НайтиПоИмени("РегистрСведений.канбан_ИспользуемыеСтатусыДоски");
	СоединениеИспользуемыеСтатусыДоски.ТипСоединения = ТипСоединенияСхемыЗапроса.ВНУТРЕННЕЕ;
	
	#КонецОбласти
	
	#Область ДанныеПроектов
	
	#Область Задачи_ЦветПроекта
	
	ИспользуемыеПроектыДоски = Оператор.Источники.Добавить("РегистрСведений.канбан_ИспользуемыеПроектыДоски", "ИспользуемыеПроектыДоски");
	ИспользуемыеПроектыДоски.Соединения.Очистить();
	
	КолонкаПроект = ПервыйПакет.Колонки.Найти("Проект");
	ВыбираемоеПолеПроект = КолонкаПроект.Поля[0];
	
	УсловиеСоединенияИспользуемыеПроекты = СтрШаблон(
		"%1 = ИспользуемыеПроектыДоски.Проект И ИспользуемыеПроектыДоски.Пользователь = &ТекущийПользователь", 
		ВыбираемоеПолеПроект);
	
	Оператор.Источники[0].Соединения.Добавить(ИспользуемыеПроектыДоски, УсловиеСоединенияИспользуемыеПроекты);
	
	СоединениеИспользуемыеПроектыДоски = Оператор.Источники[0].Соединения.НайтиПоИмени("РегистрСведений.канбан_ИспользуемыеПроектыДоски");
	СоединениеИспользуемыеПроектыДоски.ТипСоединения = ТипСоединенияСхемыЗапроса.ЛевоеВнешнее;
	
	ВыбираемоеПолеЦветПроекта = Оператор.ВыбираемыеПоля.Добавить("ЕСТЬNULL(ИспользуемыеПроектыДоски.Цвет, """")");
	КолонкаЦветПроекта = ПервыйПакет.Колонки.Найти(ВыбираемоеПолеЦветПроекта);
	КолонкаЦветПроекта.Псевдоним = "ЦветПроекта";
	
	#КонецОбласти
	
	Оператор.Отбор.Добавить(СтрШаблон("%1 В (&Проекты)", ВыбираемоеПолеПроект));
	
	Если Константы.канбан_ИспользоватьСопоставлениеСтатусов.Получить() 
		И ПервыйПакет.Операторы.Количество() > 1 Тогда
		
		Оператор2 = ПервыйПакет.Операторы[1];
		
	#Область Ошибки_ЦветПроекта
		
		ИспользуемыеПроектыДоски2 = Оператор2.Источники.Добавить("РегистрСведений.канбан_ИспользуемыеПроектыДоски", "ИспользуемыеПроектыДоски2");
		ИспользуемыеПроектыДоски2.Соединения.Очистить();
		
		ВыбираемоеПолеПроект2 = КолонкаПроект.Поля[1];
		
		УсловиеСоединенияИспользуемыеПроекты2 = СтрШаблон(
			"%1 = ИспользуемыеПроектыДоски2.Проект И ИспользуемыеПроектыДоски2.Пользователь = &ТекущийПользователь", 
			ВыбираемоеПолеПроект2);
		
		Оператор2.Источники[0].Соединения.Добавить(ИспользуемыеПроектыДоски2, УсловиеСоединенияИспользуемыеПроекты2);
		
		СоединениеИспользуемыеПроектыДоски2 = Оператор2.Источники[0].Соединения.НайтиПоИмени("РегистрСведений.канбан_ИспользуемыеПроектыДоски");
		СоединениеИспользуемыеПроектыДоски2.ТипСоединения = ТипСоединенияСхемыЗапроса.ЛевоеВнешнее;
		
		ВыбираемоеПолеЦветПроекта2 = Оператор2.ВыбираемыеПоля.Добавить("ЕСТЬNULL(ИспользуемыеПроектыДоски2.Цвет, """")");
		
		Оператор2.Отбор.Добавить(СтрШаблон("%1 В (&Проекты)", ВыбираемоеПолеПроект2));
		
	#КонецОбласти
		
	#Область Ошибки_Статус
		
		СопоставлениеСтатусов = Оператор2.Источники.Добавить("РегистрСведений.канбан_СопоставлениеСтатусов", "канбан_СопоставлениеСтатусов");
		СопоставлениеСтатусов.Соединения.Очистить();
		
		КолонкаСтатус = ПервыйПакет.Колонки.Найти("СтатусЗадачи");
		ПолеСоединенияСтатус = КолонкаСтатус.Поля[1];
		
		УсловиеСоединенияСтатусыОшибок = СтрШаблон("%1 = канбан_СопоставлениеСтатусов.СтатусОшибки", ПолеСоединенияСтатус);
		
		Оператор2.Источники[0].Соединения.Добавить(СопоставлениеСтатусов, УсловиеСоединенияСтатусыОшибок);
		
		// Заменяем выбираемое поле на статус задачи
		ВыражениеСтатусЗадачи = Новый ВыражениеСхемыЗапроса("канбан_СопоставлениеСтатусов.СтатусЗадачи");
		ИндексПоляСоединенияСтатус = Оператор2.ВыбираемыеПоля.Индекс(ПолеСоединенияСтатус);
		Оператор2.ВыбираемыеПоля[ИндексПоляСоединенияСтатус] = ВыражениеСтатусЗадачи;
		
		СоединениеСопоставлениеСтатусов = Оператор2.Источники[0].Соединения.НайтиПоИмени("РегистрСведений.канбан_СопоставлениеСтатусов");
		СоединениеСопоставлениеСтатусов.ТипСоединения = ТипСоединенияСхемыЗапроса.Внутреннее;
		
	#КонецОбласти
		
	КонецЕсли;
	
	#КонецОбласти
	
	ТекстЗапросаДанныеЗадач = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

// Заполнить всеми проектами.
// 
// Параметры:
//  ТекстЗапросаДанныеЗадач - Строка
//  Проекты - см. НовыйПроектыДоски
//
Процедура ЗаполнитьВсемиПроектами(Знач ТекстЗапросаДанныеЗадач, Проекты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеЗадач;
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаПроектов = РезультатЗапроса.Выгрузить();
	ТаблицаПроектов.Свернуть("Проект, НаименованиеПроекта, УникальныйИдентификаторПроекта");
	Для Каждого СтрокаПроекта Из ТаблицаПроектов Цикл
		
		НоваяСтрокаПроектов = Проекты.Добавить();
		НоваяСтрокаПроектов.Проект = СтрокаПроекта.Проект;
		НоваяСтрокаПроектов.ПредставлениеПроекта = СтрокаПроекта.НаименованиеПроекта;
		НоваяСтрокаПроектов.ИдентификаторПроектаСтрока = Строка(СтрокаПроекта.УникальныйИдентификаторПроекта);
		
	КонецЦикла;
	
КонецПроцедуры

// Задача по идентификатору и имени менеджера.
//
// Параметры:
//  ИдентификаторЗадача			 - Строка
//  ИмяМенеджераОбъектаЗадача	 - Строка
// 
// Возвращаемое значение:
//   - ОпределяемыйТип.канбан_Задачи, ОпределяемыйТип.канбан_Ошибки - ссылка на задачу или ошибку
//
Функция ЗадачаСсылкаПоИдентификатору(Знач ИдентификаторЗадача, Знач ИмяМенеджераОбъектаЗадача)
	
	УсловиеПроверки = Не ПустаяСтрока(ИдентификаторЗадача) И Не ПустаяСтрока(ИмяМенеджераОбъектаЗадача);
	ОбщегоНазначенияКлиентСервер.Проверить(
		УсловиеПроверки, 
		НСтр("ru = 'Не все входные параметры заполнены'"), 
		"ОбщийМодуль.канбан_КанбанДоска.ЗадачаСсылкаПоИдентификатору");
	
	ИдентификаторЗадачаГуид = Новый УникальныйИдентификатор(ИдентификаторЗадача);
	
	МенеджерОбъектаЗадача = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМенеджераОбъектаЗадача);
	ЗадачаСсылка = МенеджерОбъектаЗадача.ПолучитьСсылку(ИдентификаторЗадачаГуид);
	
	Возврат ЗадачаСсылка;
	
КонецФункции

// Возвращает статус задачи или статус ошибки по идентификатору и менеджеру объекта.
//
// Параметры:
//   ЗадачаСсылка - ОпределяемыйТип.канбан_Задачи, ОпределяемыйТип.канбан_Ошибки - ссылка на задачу или ошибку
//   ИдентификаторСтатуса - Строка - имя перечисления или уникальный идентификатор статуса
//   ИмяМенеджераОбъектаСтатус - Строка - полное имя менеджера объекта статуса
//
// Возвращаемое значение:
//   ОпределяемыйТип.канбан_СтатусыОшибок, ОпределяемыйТип.канбан_СтатусыЗадач - статус задачи или ошибки
//
Функция НовыйСтатусПоИдентификаторуИМенеджеруОбъекта(ЗадачаСсылка, ИдентификаторСтатуса, ИмяМенеджераОбъектаСтатус)
	
	УсловиеПроверки = 
		Не ЗадачаСсылка.Пустая() 
		И Не ПустаяСтрока(ИдентификаторСтатуса) 
		И Не ПустаяСтрока(ИмяМенеджераОбъектаСтатус);
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		УсловиеПроверки, 
		НСтр("ru = 'Не все входные параметры заполнены'"), 
		"ОбщийМодуль.канбан_КанбанДоска.НовыйСтатусПоИдентификаторуИМенеджеруОбъекта");
	
	МенеджерОбъектаСтатусы = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМенеджераОбъектаСтатус);
	СтатусЗадачи = МенеджерОбъектаСтатусы.ПустаяСсылка(); // ОпределяемыйТип.канбан_СтатусыЗадач;
	
	Если ОбщегоНазначения.ЭтоСправочник(СтатусЗадачи.Метаданные()) Тогда
		
		ИдентификаторСтатусГуид = Новый УникальныйИдентификатор(ИдентификаторСтатуса);
		СтатусЗадачи = МенеджерОбъектаСтатусы.ПолучитьСсылку(ИдентификаторСтатусГуид);// ОпределяемыйТип.канбан_СтатусыЗадач;
		
	ИначеЕсли ОбщегоНазначения.ЭтоПеречисление(СтатусЗадачи.Метаданные()) Тогда
		
		СтатусЗадачи = МенеджерОбъектаСтатусы[ИдентификаторСтатуса]; // ОпределяемыйТип.канбан_СтатусыЗадач;
		
	КонецЕсли;
	
	НовыйСтатус = СтатусЗадачи;
	
	ИспользоватьСопоставлениеСтатусов = Константы.канбан_ИспользоватьСопоставлениеСтатусов.Получить();
	ЭтоТипСтатусОшибки = Метаданные.ОпределяемыеТипы.канбан_Ошибки.Тип.СодержитТип(ТипЗнч(ЗадачаСсылка));
	
	Если ЭтоТипСтатусОшибки И ИспользоватьСопоставлениеСтатусов Тогда
		
		ТипОшибки = Метаданные.ОпределяемыеТипы.канбан_СтатусыОшибок.Тип.Типы()[0];
		СтатусОшибки = Новый (ТипОшибки); // ОпределяемыйТип.канбан_СтатусыОшибок;
		
		канбан_КанбанДоскаПереопределяемый.СтатусПоИдентификаторуИМенеджеруОбъекта(
			МенеджерОбъектаСтатусы, ИдентификаторСтатуса, СтатусЗадачи, СтатусОшибки);
		
		СопоставленныйСтатус(СтатусЗадачи, СтатусОшибки);
		
		Если Не СтатусОшибки.Пустая() Тогда
			
			НовыйСтатус = СтатусОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НовыйСтатус;
	
КонецФункции



#КонецОбласти
