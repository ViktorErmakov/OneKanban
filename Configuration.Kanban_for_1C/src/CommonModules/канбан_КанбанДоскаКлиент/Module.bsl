// @strict-types


#Область ПрограммныйИнтерфейс

// Открыть канбан доску
Процедура ОткрытьКанбанДоску() Экспорт
	ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.Доска");
КонецПроцедуры

// Открыть страницу разработки.
Процедура ОткрытьСтраницуРазработки() Экспорт
	ЗапуститьПриложение("https://github.com/ViktorErmakov/Kanban_for_1C");
КонецПроцедуры

// Открыть форму настройки доски.
Процедура ОткрытьФормуНастройкиДоски() Экспорт
	ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.НастройкиДоски");
КонецПроцедуры

// Отправить ответ.
// 
// Параметры:
//  ЭлементПолеHTML - ЭлементФормаHTML
//  Событие - Строка
//  Ответ - Неопределено, Структура из см. канбан_КанбанДоскаКлиент.НовыйНастройкиДоски - ответ
//
Процедура ОтправитьОтвет(Знач ЭлементПолеHTML, Знач Событие, Знач Ответ) Экспорт
	
	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементHTML = ЭлементПолеHTML.Документ.defaultView;
	Прокси = ЭлементHTML["V8Proxy"];
	
	Пакет = "";
	Если ЗначениеЗаполнено(Ответ) Тогда
		
		Если    ТипЗнч(Ответ) = Тип("Структура")
			Или ТипЗнч(Ответ) = Тип("ФиксированнаяСтруктура")
			Или ТипЗнч(Ответ) = Тип("Соответствие")
			Или ТипЗнч(Ответ) = Тип("ФиксированноеСоответствие")
			Или ТипЗнч(Ответ) = Тип("Массив")
			Или ТипЗнч(Ответ) = Тип("ФиксированныйМассив") Тогда
			
			Пакет = ЗаписатьЗначениеJSON(Ответ);
			
		ИначеЕсли ТипЗнч(Ответ) = Тип("ДвоичныеДанные") Тогда
			
			Пакет = Base64Строка(Ответ);
			
		Иначе
			
			Пакет = XMLСтрока(Ответ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Прокси.sendResponse(Событие, Пакет);
	
КонецПроцедуры

// Новый данные задачи для ответа.
// 
// Возвращаемое значение:
//  Структура - Новый данные задачи для ответа:
// * project - Строка
// * user - Строка
// * user_name - Строка
// * idTask - Строка
// * fullnameobjecttask - Строка
// * card__link_href - Строка
// * card__link_name - Строка
// * card__photo - Строка
// * alt - Строка
// * card__text - Строка
// * status - Строка
//
Функция НовыйДанныеЗадачиДляДоски() Экспорт
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("project", "");
	ДанныеОтвета.Вставить("user", "");
	ДанныеОтвета.Вставить("user_name", "");
	ДанныеОтвета.Вставить("idTask", "");
	ДанныеОтвета.Вставить("fullnameobjecttask", "");
	ДанныеОтвета.Вставить("card__link_href", "");
	ДанныеОтвета.Вставить("card__link_name", "");
	ДанныеОтвета.Вставить("card__photo", "");
	ДанныеОтвета.Вставить("alt", "");
	ДанныеОтвета.Вставить("card__text", "");
	ДанныеОтвета.Вставить("status", "");
	
	Возврат ДанныеОтвета;
	
КонецФункции

// Сохранить текущие настройки доски.
// 
// Параметры:
//  НастройкиДоски - см. НовыйНастройкиДоски
//
Процедура СохранитьТекущиеНастройкиДоски(НастройкиДоски) Экспорт
	
	КлючОбъекта = КлючОбъектаНастроекДоски();
	КлючНастроек = КлючНастроекДоски();
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, НастройкиДоски);
	
КонецПроцедуры

// Загрузить текщие настройки доски.
// 
// Возвращаемое значение:
//  см. НовыйНастройкиДоски
//
Функция ЗагрузитьТекщиеНастройкиДоски() Экспорт
	
	КлючОбъекта = КлючОбъектаНастроекДоски();
	КлючНастроек = КлючНастроекДоски();
	НастройкиДоски = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНастроек); // см. канбан_КанбанДоскаКлиент.НовыйНастройкиДоски
	
	Возврат НастройкиДоски;
	
КонецФункции

// Настройки доски.
// 
// Возвращаемое значение:
//  Структура - Новый настройки доски:
// * theme - Строка - тема доски
// * grouping - Строка - группировка доски
// * executorfilter - Массив из Строка - фильтр исполнителя
// * projectfilter - Массив из Строка - фильтр проекта
// * currentuserid - Строка - идентификатор текущего пользователя
// * currentusername - Строка - имя текущего пользователя
//
Функция НовыйНастройкиДоски() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("theme", "");
	Результат.Вставить("grouping", "");
	Результат.Вставить("executorfilter", Новый Массив);
	Результат.Вставить("projectfilter", Новый Массив);
	Результат.Вставить("currentuserid", "");
	Результат.Вставить("currentusername", "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Открыть форму списка определяемого типа.
// 
// Параметры:
//  ИмяОпределяемогоТипа - Строка
//
Процедура ОткрытьФормуСпискаОпределяемогоТипа(Знач ИмяОпределяемогоТипа) Экспорт
	
	ИмяФормы = канбан_КанбанДоскаВызовСервера.ИмяФормыСпискаОпределяемогоТипа(ИмяОпределяемогоТипа);
	
	Если Не ПустаяСтрока(ИмяФормы) Тогда
		ОткрытьФорму(ИмяФормы);
	КонецЕсли;
	
КонецПроцедуры

// События обработки оповещения.
// 
// Возвращаемое значение:
//  Массив из Строка
//
Функция СобытияОбработкиОповещения() Экспорт
	
	События = Новый Массив; // Массив из Строка
	События.Добавить("ОбновитьКанбанДоскуHTML");
	События.Добавить("канбан_ИзменениеСтатусаЗадаче");
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента();
	ИмяКонфигурации = ПараметрыРаботыКлиента.канбан_ИмяКонфигурации; // Строка
	
	канбан_КанбанДоскаКлиентПереопределяемый.СобытияОбработкиОповещения(События, ИмяКонфигурации);
	
	Возврат События;
	
КонецФункции

// Новые данные задачи и нового статуса, при изменении статуса
// 
// Возвращаемое значение:
//  Структура:
//  * ИдентификаторЗадача - Строка
//  * ИмяМенеджераОбъектаЗадача - Строка
//  * ИдентификаторСтатуса - Строка
//  * ИмяМенеджераОбъектаСтатус - Строка
//  * ИдентификаторИсполнителя - Строка
//
Функция НовыйДанныеПриИзмененииСтатуса() Экспорт
	
	ДанныеПриИзмененииСтатуса = Новый Структура;
	ДанныеПриИзмененииСтатуса.Вставить("ИдентификаторЗадача", "");
	ДанныеПриИзмененииСтатуса.Вставить("ИмяМенеджераОбъектаЗадача", "");
	ДанныеПриИзмененииСтатуса.Вставить("ИдентификаторСтатуса", "");
	ДанныеПриИзмененииСтатуса.Вставить("ИмяМенеджераОбъектаСтатус", "");
	ДанныеПриИзмененииСтатуса.Вставить("ИдентификаторИсполнителя", "");
	
	Возврат ДанныеПриИзмененииСтатуса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КлючОбъектаНастроекДоски()
	
	Возврат "OneKanban";
	
КонецФункции

Функция КлючНастроекДоски()
	
	Возврат "OneKanbanSettings";
	
КонецФункции

#КонецОбласти
